/*
Unholy Beacon by Mor'ladim, taken from his Heretic Prop Pack
Sprites for the Shockwave & Glow - Capcom (Strider 2)

Modified by Gothic
the original activated on proximity, this one activates like any switchable
decoration
*/

class UnholyBeacon : Actor 
{
bool flarespwn; bool frstticbullsh; bool onsoundplayed; bool offsoundplayed;
Default
	{
	//$Category Obstacles
	//$Title Unholy Beacon
	Radius 8;
	Height 48;
	+SOLID
	}
override void BeginPlay()
	{
	if(!bDORMANT) Activate(self);
	Super.BeginPlay();
	}
override void Activate(Actor activator)
	{
	if(frstticbullsh && !onsoundplayed)
		{
		A_SpawnItemEx("UnholyBeaconShockwave",0,0,40);
		A_StartSound("UNHBECLT");
		}
	SetStateLabel("Active");
	onsoundplayed = true; offsoundplayed = false;
	}
override void Deactivate(Actor activator)
	{
	if(frstticbullsh && !offsoundplayed)
		{
		A_SpawnItemEx("UnholyBeaconShockwave",0,0,40);
		A_StartSound("UNHBECLT");
		}
	SetStateLabel("Inactive");
	A_RemoveChildren(true,RMVF_MISC); A_RemoveLight("PropLight");
	frstticbullsh = true; onsoundplayed = false; offsoundplayed = true; flarespwn = false;
	}
override void OnDestroy()
	{
	A_RemoveChildren(true,RMVF_MISC);
	Super.OnDestroy();
	}
States
	{
	Spawn:
	Active:
		_EVP A 1 HP_BeaconLight;
		Loop;
	Inactive:
		_EVP A -1;
		Stop;
	}
void HP_BeaconLight()
	{
	if(frstticbullsh)
		{
		if(!flarespwn)
			{
			A_SpawnItemEx("UnholyBeaconGlow",0,0,37,0,0,0,0,SXF_SETMASTER|SXF_CLIENTSIDE);
			flarespwn = true;
			}
		}
	}
override void Tick()
	{
	if(!isFrozen() && !InStateSequence(self.Curstate,self.ResolveState("Inactive")))
		{
		frstticbullsh = true; A_AttachLightDef("PropLight","UnholyBeaconLight");
		}
	super.Tick();
	}
}

class UnholyBeaconShockwave : Actor
{
Default
	{
	+NoInteraction
	+ForceXYBillboard
	RenderStyle "Add";
	Alpha 0.12;
	}
States
	{
	Spawn:
		_EPG A 1 Nodelay { A_SetScale(0.5); A_SetTranslucent(0.1); }
		_EPG A 1 { A_SetScale(0.7); A_SetTranslucent(0.09); }
		_EPG A 1 { A_SetScale(0.9); A_SetTranslucent(0.08); }
		_EPG A 1 { A_SetScale(1.1); A_SetTranslucent(0.06); }
		_EPG A 1 { A_SetScale(1.3); A_SetTranslucent(0.04); }
		_EPG A 1 { A_SetScale(1.5); A_SetTranslucent(0.02); }
		Stop;
	}
}

class UnholyBeaconGlow : Actor
{
Default
	{
	+ForceXYBillboard
	+RelativeToFloor
	+NoGravity
	+Bright
	+Noclip
	RenderStyle "Add";
	Height 2;
	Radius 2;
	Scale 0.35;
	Alpha 0.5;
	}
States
	{
	Spawn:
		_EPG B 1 A_SetTranslucent(0.5,1);
		_EPG B 1 A_SetTranslucent(0.4,1);
		_EPG B 1 A_SetTranslucent(0.3,1);
		_EPG B 1 A_SetTranslucent(0.4,1);
		Loop;
	}
}