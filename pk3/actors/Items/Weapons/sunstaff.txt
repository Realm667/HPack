// Sun Staff ---------------------------------------------

ACTOR HPSunstaff : Weapon 16453
{
	//$Category Weapons
	//$Title Lightbringer
	+WEAPON.NOAUTOFIRE
	Weapon.SisterWeapon "HPSunstaffPowered"
	Weapon.SelectionOrder 2000
	Weapon.Kickback 0
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 50
	Weapon.AmmoType1 "SunStaffAmmo"
	Weapon.YAdjust 0
	Obituary "$OB_MPSUNSTAFF"
	Inventory.PickupMessage "$TXT_WPNSUNSTAFF"
	Tag "$TAG_SUNSTAFF"
	States
	{
	Spawn:
		WSUN A -1 light(LWANDAMMO)
		Stop
	Ready:
		SUNS AAABBBCCCDDD 1 BRIGHT A_WeaponReady
		Loop
	Deselect:
		SICL A 0 bright A_StopSoundEx("Weapon")
		SUNS KK 1 bright A_Lower
		SUNS LLMMNNNNNNNNN 1 bright A_Lower
		Loop
	Select:
		SUNS NNNNNNNNNNNMMLL 1 bright A_Raise
		Loop
	Fire:
		SUNS A 0 A_PlaySound("weapons/sunstaffstart")
		SUNS A 0 A_PlayWeaponSound("weapons/sunstaffbeam1")
		SUNS KKII 1 BRIGHT A_FireCustomMissile("SunFake2",0,0,0)
		SUNS E 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS E 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS A 0 A_PlaySoundEx("weapons/sunstaffbeam2","Weapon",1)
		SUNS A 0 A_Refire
		Goto Release
	Hold:
		SUNS E 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS E 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS A 0 A_Refire
	Release:
		SUNS A 0 A_PlayWeaponSound("weapons/sunstaffbeam3")
		SUNS JJ 1 BRIGHT A_FireCustomMissile("SunFake2",0,0,0)
		SUNS KD 2 BRIGHT
		Goto Ready
	}
}

ACTOR HPSunStaffPowered : HPSunStaff
{
	+WEAPON.POWERED_UP
	Tag "$TAG_SUNSTAFF"
	Weapon.SisterWeapon "HPSunStaff"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse1 25
	States
	{
	Fire:
		SUNS A 0 A_JumpIfNoAmmo("Ready")
		SUNS A 0 A_PlayWeaponSound("Weapons/sunstaffcharge")
		SUNS BCDABCDABCDABCD 2 BRIGHT 
		SUNS EFGHEFGHEFGH 2 BRIGHT 
		SUNS I 4 BRIGHT 
		SUNS N 0 A_SetBlend("FF FF 88", 1.0, 35)
		SUNS A 0 A_PlayWeaponSound("Weapons/sunstaffpowshoot")
		SUNS N 2 Offset (0,48) A_FireCustomMissile("SunMeltdown", 0, 1, 0, 0)
		SUNS N 2 Offset (0,40)
		SUNS N 2 Offset (0,36)
		SUNS N 2 Offset (0,34)
		SUNS N 10 Offset (0,32) 
		SUNS N 10 A_PlaySound("weapons/sunstaffnew") 
		SUNS ML 3
		SUNS KABCD 3 BRIGHT
		Goto Ready
    Flash:
		TNT1 A 1 A_LightInverse
		TNT1 A 0 A_Light0
		Stop
	}
}

actor SunBeam : FastProjectile
{
	PROJECTILE
	+PAINLESS
	+RIPPER
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	-BLOODSPLATTER
	radius 6
	height 4
	speed 100
	damage (random(1,2))
	alpha 1.0
	scale .9
	renderstyle Add
	seesound ""
	deathsound ""
	Damagetype Solar
	states
	{
	Spawn:
		//SUNP AB 1 BRIGHT Light(SUNSTAFFFX) A_CustomMissile("SunBlur",0,0,180)
		TNT1 A 1 BRIGHT Light(SUNSTAFFFX) A_CustomMissile("SunSpot",0,0,180)
		Wait
	Death:
		SUND ABCDEF 2 bright Light(SUNSTAFFFX) 
		Stop
	}
}

Actor SunFake  : SunBeam { Damage 0 }
Actor SunFake2 : SunFake
{
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT Light(SUNSTAFFFX) A_CustomMissile("SunSpot2",0,0,180)
		Wait
	}
}

Actor SunBlur
{
	height 2
	radius 6
	damage 0
	speed 0.2
	renderstyle Add
	alpha 0.9
	scale 1.5
	+FORCEXYBILLBOARD
	+NOGRAVITY
	+DROPOFF
	states
	{
	Spawn:
		SUNP A 2 BRIGHT Light(SUNSTAFFFX) A_FadeOut(0.2)
		Loop
	}
}

Actor SunSpot : SunBlur
{
	States
	{
	Spawn:
		SUNP BB 1 BRIGHT Light(SUNSTAFFFX) 
		Stop
	}
}

Actor SunSpot2 : SunBlur
{
	states
	{
	Spawn:
		SUNP AA 1 BRIGHT Light(SUNSTAFFFX) 
		Stop
	}
}

ACTOR SunMeltdown
{
	PROJECTILE
	radius 16
	height 8
	speed 0
	Damagetype Solar
	states
	{
		Spawn:
			TNT1 A 0
		Death:
			TNT1 A 1 A_BFGSpray("SunBurst",40,15)
			stop
	}
}

ACTOR SunBurst
{
	Radius 8
	Height 16
	RenderStyle Add
	+NOGRAVITY
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		SUNM G 0 Bright
		SUNM G 0 Bright A_PlaySound("weapons/sunstaffSphereExp")
		SUNM GGGG 0 A_SpawnItemEx("SunRay",0,0,0,8,0,random(4,16),random(1,360),160,0)
		SUNM G 4 Bright Light(SUNSPHERE_X1)
		SUNM HI 4 Bright Light(SUNSPHERE_X2)
		SUNM JK 4 Bright Light(SUNSPHERE_X3)
		SUNM LM 4 Bright Light(SUNSPHERE_X4)
		SUNM NO 4 Bright Light(SUNSPHERE_X5)
		SUNM P 4
		Stop
	}
}

ACTOR SunRay
{
	Radius 5
	Height 6
	Speed 16
	Projectile
	+THRUACTORS
	-NOGRAVITY
	+FORCEXYBILLBOARD
	RenderStyle Add
	States
	{
	Spawn:
		FX01 AAABBB 1 BRIGHT Light(SUNSTAFFFX) A_SpawnItemEx("SunRayTrail",0,0,0,0,0,0,0,0)
		Loop
	Death:
		FX01 EFGH 3 BRIGHT Light(SUNSTAFFFX) 
		Stop
	}
}


ACTOR SunRayTrail
{
  +FORCEXYBILLBOARD
  +NOBLOCKMAP
  Gravity 0.125
  RenderStyle Add
  Alpha 0.4
  States
  {
  Spawn:
    FX01 EFGH 2 BRIGHT
    Stop
  }
}
