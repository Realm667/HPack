// Hellstaff -------------------------------------------------------

ACTOR HPSkull : SkullRod replaces SkullRod
{
	//$Category Weapons
	//$Title Hellstaff
	Weapon.SelectionOrder 100
	Weapon.YAdjust 10
	Weapon.SisterWeapon "HPSkullPowered"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Fire:
		HROD B 2 Bright A_FireSkullRodPL1
		HROD F 2 Bright
		HROD B 0 A_ReFire
		Goto Ready
	}
}

ACTOR HPSkullPowered : SkullRodPowered
{
	Tag "$TAG_SKULLROD"
	Weapon.SelectionOrder 100
	Weapon.YAdjust 10
	Weapon.SisterWeapon "HPSkull"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Fire:
		HROD C 2 Bright A_PlaySound("weapons/hornrodpowshoot2", CHAN_AUTO)
		HROD D 3 Bright
		HROD E 2 Bright
		HROD F 3 Bright
		HROD G 0 Bright A_PlaySound("weapons/hornrodpowshoot3", CHAN_WEAPON)
		HROD G 0 Bright A_RailAttack(0, 0, 1, "", "", RGF_SILENT|RGF_NOPIERCING, 0, "None", 0, 0, 0, 0, 32, 0, "HPHellstaffPowerSmoke")
		HROD G 4 Bright A_FireBullets(0, 0, 1, 20*random(1, 8), "HPHellstaffPowerPuff", FBF_NORANDOM)
		HROD F 2 Bright
		HROD E 3 Bright
		HROD D 2 Bright
		HROD C 2 Bright A_ReFire
		Goto Ready
	}
}

ACTOR HPHornRodFX1 : HornRodFX1 replaces HornRodFX1
{
	Speed 20
	Damage 4
	Decal "PlasmaScorchLower"
	Scale 0.75
	Alpha 0.75

	+FORCEXYBILLBOARD
	+SEEKERMISSILE
	+SCREENSEEKER

	States
	{
	Spawn:
		FX00 A 1 Bright A_SeekerMissile (9, 6, SMF_Look|SMF_CURSPEED, 75, 10)
		FX00 A 1 Bright A_SeekerMissile (9, 6, SMF_Look|SMF_CURSPEED, 75, 10)
		FX00 A 1 Bright A_SeekerMissile (9, 6, SMF_Look|SMF_CURSPEED, 75, 10)
		FX00 B 1 Bright A_SeekerMissile (9, 6, SMF_Look|SMF_CURSPEED, 75, 10)
		FX00 B 1 Bright A_SeekerMissile (9, 6, SMF_Look|SMF_CURSPEED, 75, 10)
		FX00 B 1 Bright A_SeekerMissile (9, 6, SMF_Look|SMF_CURSPEED, 75, 10)
		Loop
	}
}

ACTOR HPHellstaffPowerPuff : HornRodFX2 // here they come just in time
{
	Health 240
	ProjectileKickBack 1
	
	-MISSILE
	+PUFFGETSOWNER
	+PUFFONACTORS
	+ALWAYSPUFF

	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("weapons/hornrodpowhit", CHAN_WEAPON)
		Goto Death
	}
}

ACTOR HPHellstaffPowerSmoke // power mirrors not included in base package
{
	RenderStyle Translucent
	Alpha 0.4
	
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH

	States
	{
	Spawn:
		XRFX A 4 Bright NoDelay A_Jump(128, 1)
		XRFX BCDE 4 Bright
		Stop
	}
}

ACTOR HPRainPillar : RainPillar replaces RainPillar
{
	Damage 10
	Speed 20
	ProjectileKickBack 1
}
