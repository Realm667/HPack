// Gauntlets of D'Sparil ---------------------------------------------

ACTOR HPDSparilGauntlets : Weapon 23465
{
	//$Category Weapons
	//$Title Gloves of D'Sparil
	Weapon.SisterWeapon "HPDSparilGauntletsPowered"
	Weapon.SelectionOrder 2001
	Weapon.Kickback 25
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 5
	Weapon.AmmoGive1 10
	Weapon.YAdjust 10
	Weapon.AmmoType1 "ChaosAmmo"
	Obituary "$OB_MPDSPARILS"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$TXT_WPNDSPARILS"
	Tag "$TAG_DSPARILSG"
	States
	{
	Spawn:
		WGT3 A -1
		Stop
	Ready:
		GAU3 A 0 A_TakeInventory("GauntletHand",1)
		GAU3 A 1 A_WeaponReady
		Wait
	Deselect:
		GAU3 A 0 A_TakeInventory("GauntletHand",1)
		GAU3 A 1 A_Lower
		Wait
	Select:
		GAU3 A 0 A_TakeInventory("GauntletHand",1)
		GAU3 A 1 A_Raise
		Wait
	Fire:
		GAU3 A 3 A_PlaySound("weapons/gauntletsactivate")
		GAU3 B 3 
		GAU3 C 3 
		GAU3 D 1 BRIGHT
		GAU3 E 0 BRIGHT A_PlaySound("weapons/gauntletsshoot", CHAN_AUTO)
		GAU3 E 0 BRIGHT A_FireCustomMissile("GauntletFX1",-5.625,0,-8)
		GAU3 E 0 BRIGHT A_FireCustomMissile("GauntletFX1", 0.000,0,-8)
		GAU3 E 1 BRIGHT A_FireCustomMissile("GauntletFX1", 5.625,1,-8)
		GAU3 FGH 2
		GAU3 A 0 A_PlayWeaponSound("weapons/gauntletsrip")
		GAU3 A 2 A_Refire
		Goto Ready
	Hold:
		GAU3 A 0 A_JumpIfInventory("GauntletHand",1,"FireLeft")
	FireRight:
		GAU3 I 2 A_GiveInventory("GauntletHand",1)
		GAU3 J 1 BRIGHT
		GAU3 K 0 BRIGHT A_PlaySound("weapons/gauntletsshoot", CHAN_AUTO)
		GAU3 K 0 BRIGHT A_FireCustomMissile("GauntletFX1",-5.625,0,8)
		GAU3 K 0 BRIGHT A_FireCustomMissile("GauntletFX1", 0.000,0,8)
		GAU3 K 1 BRIGHT A_FireCustomMissile("GauntletFX1", 5.625,1,8)
		GAU3 LMN 2
		GAU3 A 0 A_PlayWeaponSound("weapons/gauntletsrip")
		GAU3 A 2 A_Refire
		Goto Ready
	FireLeft:
		GAU3 O 2 A_TakeInventory("GauntletHand",1)
		GAU3 P 1 BRIGHT
		GAU3 E 0 BRIGHT A_PlaySound("weapons/gauntletsshoot", CHAN_AUTO)
		GAU3 E 0 BRIGHT A_FireCustomMissile("GauntletFX1",-5.625,0,-8)
		GAU3 E 0 BRIGHT A_FireCustomMissile("GauntletFX1", 0.000,0,-8)
		GAU3 E 1 BRIGHT A_FireCustomMissile("GauntletFX1", 5.625,1,-8)
		GAU3 FGH 2
		GAU3 A 0 A_PlayWeaponSound("weapons/gauntletsrip")
		GAU3 A 2 A_Refire
		Goto Ready
	}
}

ACTOR GauntletHand : Inventory { Inventory.MaxAmount 3 }

ACTOR GauntletFX1 : WizardFX1
{
	Obituary "%o was seared to a crisp by %k's gauntlets."
	DeathSound "weapons/macehit"
	Damage 5
	+SEEKERMISSILE
	+FORCEXYBILLBOARD
	Decal "HImpScorch"
	States
	{
	Spawn:
		GNFX AAABBB 1 BRIGHT A_SpawnItemEx("GauntletFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		Loop
	Death:
		GNFX C 4 BRIGHT A_SpawnItemEx("GauntletFX3",0,0,0,0,0,0,0,0)
		GNFX DEFG 4 BRIGHT
		Stop
	}
}

ACTOR GauntletFX2 : GauntletPuff2
{
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		GNFX HIJK 3 BRIGHT
		Stop
	}
}

ACTOR GauntletFX3 : GauntletFX2
{
	+FORCEXYBILLBOARD
	Alpha .8
	States
	{
	Spawn:
		GNFX NO 3 BRIGHT
		GNFX PPPQQQ 1 BRIGHT A_FadeOut(0.1)
		Stop
	}
}

ACTOR HPDSparilGauntletsPowered : HPDSparilGauntlets
{
	Weapon.SisterWeapon "HPDSparilGauntlets"
	+POWERED_UP
	Weapon.Kickback 25
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 10
	Tag "$TAG_DSPARILSG"
	States
	{
	Ready:
		GAU2 A 0 A_TakeInventory("GauntletHand",1)
		GAU2 AAAQQQRRR 1 A_WeaponReady
		goto ready +1
	Deselect:
		GAU2 A 0 A_TakeInventory("GauntletHand",1)
		GAU2 A 1 A_Lower
		Wait
	Select:
		GAU2 A 0 A_TakeInventory("GauntletHand",1)
		GAU2 A 1 A_Raise
		Wait
	Fire:
		GAU2 A 3 A_PlaySound("weapons/gauntletsactivate")
		GAU2 B 3 
		GAU2 C 3 
		GAU2 D 2 BRIGHT
		GAU2 E 0 BRIGHT A_PlaySound("weapons/gauntletspowshoot", CHAN_AUTO)
		GAU2 E 0 BRIGHT A_FireCustomMissile("GauntletFX4",-5.625,0,-8)
		GAU2 E 0 BRIGHT A_FireCustomMissile("GauntletFX4", 0.000,0,-8)
		GAU2 E 2 BRIGHT A_FireCustomMissile("GauntletFX4", 5.625,1,-8)
		GAU2 FGH 2
		GAU2 A 0 A_PlayWeaponSound("weapons/gauntletsrip")
		GAU2 A 2 A_Refire
		Goto Ready
	Hold:
		GAU2 A 0 A_JumpIfInventory("GauntletHand",1,"FireLeft")
	FireRight:
		GAU2 I 2 A_GiveInventory("GauntletHand",1)
		GAU2 J 2 BRIGHT
		GAU2 K 0 BRIGHT A_PlaySound("weapons/gauntletspowshoot", CHAN_AUTO)
		GAU2 K 0 BRIGHT A_FireCustomMissile("GauntletFX4",-5.625,0,8)
		GAU2 K 0 BRIGHT A_FireCustomMissile("GauntletFX4", 0.000,0,8)
		GAU2 K 2 BRIGHT A_FireCustomMissile("GauntletFX4", 5.625,1,8)
		GAU2 LMN 2
		GAU2 A 0 A_PlayWeaponSound("weapons/gauntletsrip")
		GAU2 A 2 A_Refire
		Goto Ready
	FireLeft:
		GAU2 O 2 A_TakeInventory("GauntletHand",1)
		GAU2 P 2 BRIGHT
		GAU2 E 0 BRIGHT A_PlaySound("weapons/gauntletspowshoot", CHAN_AUTO)
		GAU2 E 0 BRIGHT A_FireCustomMissile("GauntletFX4",-5.625,0,-8)
		GAU2 E 0 BRIGHT A_FireCustomMissile("GauntletFX4", 0.000,0,-8)
		GAU2 E 2 BRIGHT A_FireCustomMissile("GauntletFX4", 5.625,1,-8)
		GAU2 FGH 2
		GAU2 A 0 A_PlayWeaponSound("weapons/gauntletsrip")
		GAU2 A 2 A_Refire
		Goto Ready
	}
}

ACTOR GauntletFX4 : GauntletFX1
{
	Alpha 1
	SeeSound "weapons/gauntletspowshoot"
	DeathSound "weapons/gauntletspowhit"
	+FORCEXYBILLBOARD
	+EXTREMEDEATH
	Decal "HImpScorch"
	States
	{
	Spawn:
		GNF2 AAABBB 1 BRIGHT A_SpawnItemEx("GauntletFX5",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		Loop
	Death:
		GNF2 C 4 BRIGHT A_SpawnItemEx("GauntletFX6",0,0,0,0,0,0,0,0)
		GNF2 D 4 BRIGHT A_Explode(40, 64, 0, 0, 64)
		GNF2 EFG 4 BRIGHT
		Stop
	}
}

ACTOR GauntletFX5 : GauntletFX2
{
	+FORCEXYBILLBOARD
	-NOGRAVITY
	Gravity 0.05
	Alpha 1
	Renderstyle Add
	States
	{
	Spawn:
		GNF2 L 8 BRIGHT
		GNF2 M 8 BRIGHT A_FadeOut(0.2)
		Stop
	}
}

ACTOR GauntletFX6 : GauntletFX3
{
	+FORCEXYBILLBOARD
	Alpha 1
	Scale 0.66
	States
	{
	Spawn:
		GNF3 DEF 2 BRIGHT
		GNF3 GHIJK 2 BRIGHT A_FadeOut(0.2)
		Stop
	}
}
