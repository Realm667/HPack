// Polymorph Wand ---------------------------------------------

ACTOR HPMorphWand : Weapon 20345
{
	//$Category Weapons
	//$Title Polymorph Wand
  Weapon.SelectionOrder 2000
  Weapon.AmmoGive 5
  Weapon.AmmoUse1 1
  Weapon.AmmoType1 "AcornAmmo"
  Weapon.YAdjust 0
  Weapon.SisterWeapon "HPMorphWandPowered"
	Inventory.PickupMessage "$TXT_WPNPOLYMORPH"
	Tag "$TAG_POLYMORPHWAND"
  //AttackSound "weapons/polyshot"
  States
  {
  Spawn:
    ANTL A -1
    Stop
  Ready:
    ANTL B 1 A_WeaponReady
    ANTL B 0 A_Jump(5, "WobbleStart")
    Loop
  WobbleStart:
    ANTL CCCCCDDDD 1 A_WeaponReady
  WobbleLoop:
    ANTL D 1 A_WeaponReady
    ANTL D 0 A_Jump(15, "WobbleEnd")
    Loop
  WobbleEnd:
    ANTL CCCCC 1 A_WeaponReady
    Goto Ready
  Deselect:
    ANTL B 1 A_Lower
    Loop
  Select:
    ANTL B 1 A_Raise
    Loop
  Fire:
    ANTL EF 3 
	ANTL GH 2 bright
    ANTL I 2 bright A_FireCustomMissile("SquirrelFX")
    ANTL FFFFE 3
	ANTL B 3
    Goto Ready
  }
}

ACTOR HPMorphWandPowered : HPMorphWand
{
  +WEAPON.POWERED_UP
  Weapon.SisterWeapon "HPMorphWand"
	Tag "$TXT_POLYMORPHWAND"
  States
  {
  Fire:
    ANTL E 0 A_JumpIfNoAmmo("Ready")
    ANTL EF 3 
	ANTL GJ 4 bright
    ANTL K 4 bright A_FireCustomMissile("SquirrelFX2")
	ANTL JI 4 bright
    ANTL FFE 3
	ANTL B 3
    Goto Ready
  }
}

ACTOR SquirrelFX : MorphProjectile
{
  +FORCEXYBILLBOARD
  Radius 8
  Height 8
  Speed 18
  RenderStyle Add
  Alpha 0.5
  SeeSound "weapons/polyshot"
  DeathSound "weapons/polyhit"
  MorphProjectile.PlayerClass "SquirrelPlayer"
  MorphProjectile.MonsterClass "Squirrel"
  MorphProjectile.Duration 3150
  MorphProjectile.MorphStyle MRF_UNDOBYTOMEOFPOWER
  States
  {
  Spawn:
    CURS ABCDEF 4 Bright
    Loop
  Death:
    CURS GHIJKL 3 Bright
    Stop
  }
}

ACTOR SquirrelFX2
{
  Radius 1
  Height 1
  Speed 3
  RenderStyle Add
  Alpha 0.8
  PROJECTILE
  +RIPPER
  +NOBOSSRIP
  +DONTREFLECT
  +BLOODLESSIMPACT
  +NOEXPLODEFLOOR
  +PAINLESS
  +SLIDESONWALLS
  +FORCEXYBILLBOARD
  SeeSound "weapons/polyshot"
  DeathSound "weapons/polyhit"
  reactiontime 30
  States
  {
  Spawn:
    CURS MNO 4 bright
	CURS P 4 bright A_Stop
	CURS QRS 4 bright
  Stay:
	CURS UVW 5 bright A_CustomMissile("SquirrelFX3", -12)
	CURS W 0 bright A_CountDown
    Loop
  Death:
    CURS GHIJKL 3 bright
    Stop
  }
}

ACTOR SquirrelFX3 : MorphProjectile
{
  Radius 16
  Height 24
  Speed 0
  RenderStyle Add
  Alpha 0.8
  DeathSound "weapons/polyhit"
  MorphProjectile.PlayerClass "SquirrelPlayer"
  MorphProjectile.MonsterClass "Squirrel"
  MorphProjectile.Duration 3150
  MorphProjectile.MorphStyle MRF_UNDOBYTOMEOFPOWER
  +FORCEXYBILLBOARD
  +NOEXPLODEFLOOR
  +SLIDESONWALLS
  States
  {
  Spawn:
    TNT1 AAAAA 1
	stop
  XDeath:
    CURS GHIJKL 3 bright
    Stop
  Death:
    TNT1 A 1
    Stop
  }
}

ACTOR Squirrel : MorphedMonster 30156
{
	//$Category Monsters
	//$Title Squirrel
  Health 10
  Radius 10
  Height 20
  Mass 40
  Speed 10
  Monster
  -COUNTKILL
  +DONTMORPH
  +FLOORCLIP
  +FRIGHTENED
  //+TOUCHY
  SeeSound "squirrel/active"
  AttackSound "squirrel/attack"
  PainSound "squirrel/pain"
  DeathSound "squirrel/death"
  ActiveSound "squirrel/active"
  Obituary "%o was bitten to death."
  States
  {
  Spawn:
    SQRL AA 10 A_Look
    Loop
  See:
    SQRL ABC 3 A_Chase
	SQRL A 0 A_ChangeFlag("TOUCHY",1)
    Loop
  Melee:
    SQRL A 6
	SQRL B 6 A_CustomMeleeAttack(random(1,8))
    Goto See
  Death:
    SQRL D 6 A_Scream
    SQRL E 6 A_NoBlocking
    SQRL F 6
    Stop
  }
}

ACTOR SquirrelPlayer : PlayerPawn
{
  Health 30
  ReactionTime 0
  PainChance 255
  Radius 10
  Height 20
  Speed 1
  +NOSKIN
  PainSound "squirrel/pain"
  DeathSound "squirrel/death"
  Player.JumpZ 3
  Player.Viewheight 18
  Player.ForwardMove 1.22, 1.22
  Player.SideMove 1.22, 1.22
  Player.SpawnClass "Squirrel"
  Player.SoundClass "Squirrel"
  Player.DisplayName "Squirrel"
  Player.MorphWeapon "SquirrelNose"
  -PICKUP
  States
  {
  Spawn:
    SQRL A -1
    Stop
  See:
    SQRL ABC 3
    Loop
  Melee:
  Missile:
    SQRL C 12
    Goto Spawn
  Pain:
    SQRL C 4 A_Pain
    Goto Spawn
  Death:
	SQRL D 3 A_Scream
    SQRL E 3 A_NoBlocking
    SQRL F 3
	TNT1 A 35 A_CheckPlayerDone
    Wait
  }
}

ACTOR SquirrelNose : Beak
{
  Weapon.SelectionOrder 10000
  +WEAPON.DONTBOB
  +WEAPON.MELEEWEAPON
  Weapon.YAdjust 15
  Weapon.SisterWeapon "SquirrelNosePowered"
  States
  {
  Ready:
    SQRL GGGGGG 1 A_WeaponReady
	SQRL G 0 A_Jump(16,1)
    Loop
	SQRL H 1 A_PlaySound("squirrel/sight")
	SQRL HHH 1
	Loop
  Deselect:
    SQRL G 1 A_Lower
    Loop
  Select:
    SQRL G 1 A_Raise
    Loop
  Fire:
    SQRL H 1 A_PlaySound("rat/attack")
    SQRL H 5 A_CustomPunch(2)
	SQRL G 3
    Goto Ready
  }
}

ACTOR SquirrelNosePowered : SquirrelNose
{
  +WEAPON.POWERED_UP
  Weapon.SisterWeapon "SquirrelNose"
}