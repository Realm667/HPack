// Firemace ---------------------------------------------

ACTOR HPMace : Mace replaces MaceSpawner
{
	//$Category Weapons
	//$Title Firemace
	Tag "$TAG_MACE"
	Weapon.SelectionOrder 1300
	Weapon.SisterWeapon "HPMacePowered"
	Weapon.SlotPriority 0
	Obituary "$OB_MPMACE"

	States
	{
	Fire:
		MACE B 2
	Hold:
		MACE C 0 A_PlaySoundEx("weapons/macehit", CHAN_WEAPON)
		MACE C 2 Offset(-1, 32) A_FireCustomMissile("HPMaceBall", frandom(-4.5, 4.5), 1, 0, 0, CMF_AIMOFFSET, frandom(-1.5, 1.5))
		MACE D 2 Offset(-3, 31) A_FireCustomMissile("HPMaceBall", frandom(-4.5, 4.5), 0, 0, 0, CMF_AIMOFFSET, frandom(-1.5, 1.5))
		MACE E 0 A_PlaySoundEx("weapons/macehit", CHAN_WEAPON)
		MACE E 2 Offset(-1, 30) A_FireCustomMissile("HPMaceBall", frandom(-4.5, 4.5), 1, 0, 0, CMF_AIMOFFSET, frandom(-1.5, 1.5))
		MACE F 2 Offset( 1, 31) A_FireCustomMissile("HPMaceBall", frandom(-4.5, 4.5), 0, 0, 0, CMF_AIMOFFSET, frandom(-1.5, 1.5))
		MACE B 3 Offset( 1, 32) A_ReFire
		Goto Ready
	}
}

ACTOR HPMacePowered : MacePowered
{
	Tag "$TAG_MACE"
	Weapon.SelectionOrder 1300
	Weapon.SisterWeapon "HPMace"
	Weapon.AmmoUse 2
	Obituary "$OB_MPMACE"

	States
	{
	Fire:
		MACE B 2
	Hold:
		MACE F 0 A_PlaySoundEx("weapons/macepowhit", CHAN_WEAPON)
		MACE F 2 Offset(0, 38) A_FireCustomMissile("HPBigMaceBall", frandom(-2.5, 2.5), 1, 0, 0)
		MACE F 2 Offset(0, 36)
		MACE B 2 Offset(0, 34)
		MACE A 2 Offset(0, 33)
		MACE A 0 Offset(0, 32) A_ReFire
		Goto Ready
	}
}

Actor HPMaceBall
{
	Radius 8
	Height 6
	Speed 25
	Damage 2
	Obituary "$OB_MPMACE"
	SeeSound "weapons/maceshoot"
	DeathSound "weapons/macehit"
	BounceSound "weapons/macebounce"
	BounceCount 6
	BounceFactor 1.0
	WallBounceFactor 1.0
	Projectile

	+BOUNCEONWALLS
	+BOUNCEONCEILINGS
	+BOUNCEONFLOORS
	+THRUGHOST

	States
	{
	Spawn:
		FX02 AABB 2 A_SpawnItemEx("HPMaceTrailSmall")
		Loop
	Death:
		FX02 F 2 Bright
		FX02 GHIJ 3 Bright
		Stop
	}
}

Actor HPBigMaceBall : HPMaceBall
{
	Damage 15
	ProjectileKickback 1
	DeathSound "weapons/macepowhit"

	+SEEKERMISSILE
	+USEBOUNCESTATE

	States
	{
	Spawn:
		FX02 CCDD 2 A_SpawnItemEx("HPMaceTrail")
		Loop
	Bounce:
		FX02 AA 0 A_SeekerMissile(90, 90, SMF_LOOK, 256)
		Goto Spawn
	}
}

Actor HPMaceTrail : Puffy
{
	States
	{
	Spawn:
		TNT1 A 2
		FRB1 DEFGH 2 Bright
		Stop
	}
}

Actor HPMaceTrailSmall : HPMaceTrail
{
	Scale 0.5
}
