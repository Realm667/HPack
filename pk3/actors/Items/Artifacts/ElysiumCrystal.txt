// WAND CRYSTAL
Actor ArtiWandCrystal : CustomInventory 19998
{
	//$Category Artifacts
	//$Title Elysium Crystal
	+COUNTITEM
	+FLOATBOB
	+INVENTORY.INVBAR
	+INVENTORY.PICKUPFLASH
	+INVENTORY.FANCYPICKUPSOUND
	Inventory.Icon "ARTIWAND"
	Inventory.DefMaxAmount
	Inventory.PickupSound "misc/p_pkup"
	Inventory.UseSound "misc/invuse"
	Inventory.PickupMessage "$TXT_ARTIWANDCRYSTAL"
	Tag "$TAG_ARTIWANDCRYSTAL"
	States
	{
	Spawn:
		AWND A -1
		Stop
	Use:
		TNT1 A 0 A_SetArg(0, 0)	// main ring angle iterator
		TNT1 A 0 A_SetArg(1, 0)	// main ring angle increment
		TNT1 A 0 A_SetArg(2, 0)	// extra ring angle iterator
		TNT1 A 0 A_SetArg(3, 0)	// extra ring angle increment
		TNT1 A 0 A_SetArg(4, 0)	// extra ring pitch
		TNT1 A 0 A_JumpIfInventory("BagOfHolding", 1, "ICanHasBackPack")
		Goto NoBackPack
	ICanHasBackPack:
		// A_JumpIfInventory always return true if the owned amount is the maxamount, 
		// even if it falls short of the requested amount. So this trickery is necessary
		// to prevent people without backpacks to access the upper use levels.
		TNT1 A 0 A_JumpIfInventory("GoldWandAmmo", 200, "UseLevel5")
		TNT1 A 0 A_JumpIfInventory("GoldWandAmmo", 150, "UseLevel4")
		// Fallthrough
	NoBackPack:
		TNT1 A 0 A_JumpIfInventory("GoldWandAmmo", 100, "UseLevel3")
		TNT1 A 0 A_JumpIfInventory("GoldWandAmmo",  50, "UseLevel2")
		// Fallthrough
	UseLevel1:
		TNT1 A 0 A_SetArg(1, 30)
		Goto UseLoop
	UseLevel2:
		TNT1 A 0 A_SetArg(1, 20)
		Goto UseLoop
	UseLevel3:
		TNT1 A 0 A_SetArg(1, 15)
		TNT1 A 0 A_SetArg(3, 30)
		TNT1 A 0 A_SetArg(4, 10)
		Goto UseLoop
	UseLevel4:
		TNT1 A 0 A_SetArg(1, 10)
		TNT1 A 0 A_SetArg(3, 20)
		TNT1 A 0 A_SetArg(4, 10)
		Goto UseLoop
	UseLevel5:
		TNT1 A 0 A_SetArg(1, 5)
		TNT1 A 0 A_SetArg(3, 10)
		TNT1 A 0 A_SetArg(4, 10)
		Goto UseLoop
	UseLoop:
		TNT1 A 0 A_FireCustomMissile("GoldWandFX1", args[0], false, 0, 8, false, 0)
		TNT1 A 0 A_SetArg(0, (args[0]+args[1])%360)
		TNT1 A 0 A_JumpIf(Args[3]==0, "UseLoopCheck")
		TNT1 A 0 A_FireCustomMissile("GoldWandFX1", args[2], false, 0, 8, false, args[4])
		TNT1 A 0 A_FireCustomMissile("GoldWandFX1", args[2], false, 0, 8, false, -args[4])
		TNT1 A 0 A_SetArg(2, (args[2]+args[3])%360)
	UseLoopCheck:
		TNT1 A 0 A_JumpIf(args[0]==0, "UseEnd")
		Goto UseLoop
	UseEnd:
		TNT1 A 0 A_JumpIf(((args[1]==30) || (args[1]==0)), 1)
		TNT1 A 0 A_TakeInventory("GoldWandAmmo", ((180/args[1])-6), TIF_NOTAKEINFINITE)
		TNT1 A 0
		Stop
	}
}

