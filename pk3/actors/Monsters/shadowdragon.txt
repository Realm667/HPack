ACTOR ShadowDragon : Beast 14563
{
	//$Category Monsters
	//$Title Shadow Dragon
  Health 600
  Scale 1.2
  Radius 24
  Height 74
  Speed 14
  PainChance 100
  Mass 200
  MONSTER 
  +FLOORCLIP
  +TELESTOMP
  Obituary "$OB_BEASTSHADOW"
  states
  {
  Spawn:
   SHAD AB 10 A_Look
   loop
  See:
   SHAD ABCDEF 3 A_Chase
   loop
  Missile:
   SHAD H 10 A_FaceTarget
   SHAD I 0 A_CustomMissile ("RedShadowBall", 64, 0, 0, 1)
   SHAD I 0 A_CustomMissile ("RedShadowBall", 64, 0, -8, 1)
   SHAD I 10 A_CustomMissile ("RedShadowBall", 64, 0, 8, 1)
   Goto See+3
  Pain:
   SHAD G 3
   SHAD G 3 A_Pain
   goto See
  Death:
   SHAD R 6
   SHAD S 6 A_Scream
   SHAD TUV 6
   SHAD W 6 A_NoBlocking
   SHAD XY 6
   SHAD Z -1
   stop
  XDeath:
   SHAD J 5
   SHAD K 6 A_Scream
   SHAD L 5
   SHAD M 6
   SHAD N 5
   SHAD O 6 A_NoBlocking
   SHAD P 5
   SHAD Q -1
   stop
  }
}

ACTOR RedShadowBall : BeastBall
{
  Radius 9
  Height 8
  Speed 12
  FastSpeed 20
  Damage 4
  Projectile
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  -NOBLOCKMAP
  +WINDTHRUST
  +SPAWNSOUNDSOURCE
  Damage 4
  RenderStyle Add
  SeeSound "ShDragon/attack"
  DeathSound "DemonMissileExplode"
  States
  {
     Spawn:
        SHAX AABBCC 2 A_SpawnItemEx("RedShadowPuffy", random2[BeastPuff]()*0.015625, random2[BeastPuff]()*0.015625, 
        random2[BeastPuff]()*0.015625, 0,0,0,0,SXF_ABSOLUTEPOSITION, 64)
        Loop
     Death:
        SHAX IJKLMN 4
        Stop
  }
}

ACTOR RedShadowPuffy
{
  radius 6
  height 8
  speed 10
  damage 0
  renderstyle Add
  +NOBLOCKMAP
  +NOGRAVITY
  +MISSILE
  +NOTELEPORT
  +DONTSPLASH
  states
  {
  Spawn:
    SHAX DEFGH 4
    stop
  }
}
