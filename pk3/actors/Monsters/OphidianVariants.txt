actor HPSnakeWeak : HPSnake 16603
{
	//$Category Monsters
	//$Title Ophidian Offspring
	Health 100
	Mass 50
	Translation "66:136=%[0.0, 0.0, 0.0]:[1.8, 0.8, 0.1]"
	Scale 0.8
	DropItem "GoldWandAmmo", 84, 10
	Obituary "$OB_SNAKEWEAK"
	Tag "$FN_SNAKEWEAK"
	SeeSound "OphidianBaby/Sight"
	ActiveSound "OphidianBaby/Active"
	PainSound "OphidianBaby/Pain"
	DeathSound "OphidianBaby/Death"
	States
	{
	Missile:
		SNKE F 5 A_FaceTarget
		SNKE F 4 A_CustomMissile("SnakeProjC", 32, 0, 0, CMF_CHECKTARGETDEAD)
		Goto See
	}
}

actor SnakeProjC : SnakeProjB
{
	Damage 1
}

actor HPSnakeStrong : HPSnake 16604
{
	//$Category Monsters
	//$Title Ophidian Lord
	Health 500
	Mass 500
	Speed 14
	Translation "66:68=254:252", "69:94=225:240", "95:110=209:224", "111:136=209:224"
	Radius 24
	Height 96
	Scale 1.2
	DropItem "BlasterAmmo", 84, 10
	Obituary "$OB_SNAKESTRONG"
	Tag "$FN_SNAKESTRONG"
	SeeSound "OphidianLord/Sight"
	ActiveSound "OphidianLord/Active"
	PainSound "OphidianLord/Pain"
	DeathSound "OphidianLord/Death"
	States
	{
	Missile:
		SNKE F 10 A_FaceTarget
		SNKE F 0 bright A_PlaySound("array/firep")
		SNKE F 0 A_FaceTarget
		SNKE F 10 bright A_CustomMissile("ForceShot", 56)
		SNKE F 0 A_FaceTarget
		SNKE F 10 bright A_CustomMissile("ForceShot", 56)
		SNKE F 0 A_FaceTarget
		SNKE F 10 bright A_CustomMissile("ForceShot", 56)
		Goto See
	}
}

ACTOR ForceShot
{
	Projectile
	Radius 6
	Height 6
	Speed 30
	Damage 4
	Alpha 0.5
	Scale 2
	RenderStyle Add
	DeathSound "array/impactp"
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		RADE AE 2 Bright Light("FORCEBLAST_X2") A_SpawnItemEx("ForceTrail",0,0,0,0,0,0,0,8)
		Loop
	Death:
		// 8-way particles:
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,-7,-7)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,-7,7)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,7,7)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,7,-7)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,14,0)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,0,14)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,-14,0)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,0,-14)
		// Alternate height 8-way:
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,-5,-5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,-5,5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,5,5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,5,-5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,10,0,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,0,10,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,-10,0,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail",0,0,0,0,-10,5)
		RADE A 2 Bright Light("FORCEBLAST_X2")
		RADE BCD 2 Bright Light("FORCEBLAST_X1")
		RADE EF  2 Bright Light("FORCEBLAST_X2")
		RADE GHI 2 Bright Light("FORCEBLAST_X3")
		Stop
	}
}

ACTOR ForceTrail
{   
	Radius 1
	Height 1
	Damage 0
	Speed 0
	Projectile
	RenderStyle Add
	Alpha 0.3
	Scale 1.5
	-NOGRAVITY
	+FORCEXYBILLBOARD
	+DONTSPLASH
	States
	{
	Spawn:
		RADE FGHI 2 Bright Light("FORCEBLAST_X3")
		Stop 
	}
}