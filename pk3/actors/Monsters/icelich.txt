actor IceLich : Iceguy 16248
{
	//$Category Monsters
	//$Title Ice Lich
	Health 700
	Radius 40
	Height 72
	Mass 500
	Speed 7
	PainChance 16
	DamageFactor "Poison", 0
	DamageFactor "Ice", 0
	Species "IronLich"
	Monster
	+NOGRAVITY
	+NOBLOOD
	+FLOAT
	+NOTARGETSWITCH
	+DONTHARMSPECIES
	+MISSILEMORE
	+DONTBLAST
	+DONTMORPH
	+DONTSQUASH
	+NOICEDEATH
	+DONTSPLASH
	SeeSound "icelich/sight"
	PainSound "icelich/pain"
	DeathSound "icelich/death"
	ActiveSound "icelich/active"
	AttackSound ""
	Obituary "$OB_ICELICH"
	Tag "$FN_ICELICH"
	DropItem "BlasterAmmo", 48, 16
	States
	{
	Spawn:
		ILCH A 10 A_Look
		loop
	See:
		ILCH A 0 A_StopSound(7)
		ILCH AAAA 2 A_Chase
		ILCH A 0 A_SpawnItemEx("IceGuyWisp1",random(-20,20),random(-20,20),random(50,70),0,0,0,32,128)
		ILCH AAAA 2 A_Chase
		ILCH A 0 A_SpawnItemEx("IceGuyWisp2",random(-20,20),random(-20,20),random(50,70),0,0,0,32,128)
		Loop
	Missile:
		ILCH B 0 bright A_JumpIfCloser(300,"Breath")
		ILCH A 3 bright A_FaceTarget
		ILCH B 3 bright
			{
			A_FaceTarget; A_StartSound("IceGuyAttack",6);
			A_SpawnProjectile("IceGuyFX",44,10,0,CMF_AIMOFFSET);
			A_SpawnProjectile("IceGuyFX",44,-10,0,CMF_AIMOFFSET);
			}
		ILCH BA 3 bright
		Goto See
	Breath:
		ILCH B 0 bright A_StartSound("FrostBreath",7,CHANF_LOOPING,1.0,ATTN_NORM,0,frandom(0,0.8))
		ILCH B 0 bright A_FaceTarget
		ILCH BBB 2 bright A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD)
		ILCH B 0 bright A_FaceTarget
		ILCH BBB 2 bright A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD)
		ILCH B 0 bright A_FaceTarget
		ILCH BBB 2 bright A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD)
		ILCH B 0 bright A_FaceTarget
		ILCH B 2 bright { A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD); A_SoundVolume(7,0.9); }
		ILCH B 2 bright { A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD); A_SoundVolume(7,0.8); }
		ILCH B 2 bright { A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD); A_SoundVolume(7,0.7); }
		ILCH B 0 bright A_FaceTarget
		ILCH B 2 bright { A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD); A_SoundVolume(7,0.6); }
		ILCH B 2 bright { A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD); A_SoundVolume(7,0.4); }
		ILCH B 2 bright { A_SpawnProjectile("IceBreath",20,0,random(-10,10),CMF_CHECKTARGETDEAD); A_SoundVolume(7,0.2); }
		ILCH B 0 bright A_StopSound(7)
		Goto See
	Pain:
		ILCH A 2 { A_StopSound(7); A_Pain; }
		Goto See
	Death:
		ILCH C 1 A_StopSound(7)
		ILCH C 6 A_Scream
		ILCH C 1 A_IceGuyDie
		Stop
	}
}

Actor IceBreath
{
	Radius 12
	Height 8
	Speed 10
	Damage 1
	RenderStyle Translucent
	Alpha 0.2
	Projectile
	-NOBLOCKMAP
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+WINDTHRUST
	+BLOODLESSIMPACT
	+ICEDAMAGE
	States
	{
	Spawn:
		IBRT ABABABABABABABABAB 2 Bright
		IBRT ABAB 2 Bright A_FadeOut(0.05)
	Death:
		TNT1 A 1
		Stop
	}
}
