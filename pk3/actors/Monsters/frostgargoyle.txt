Actor FrostImp : HereticImp 19283
{
	//$Category Monsters
	//$Title Frost Gargoyle
	Obituary "$OB_FROSTIMP"
	HitObituary "$OB_FROSTIMPH"
	BloodColor "A4 A4 B8"
	+NOICEDEATH //How do you freeze an ice cube? ;)
	States
	{
	Spawn:
		IIMP ABCB 10 A_Look
		Loop
	See:
		IIMP AABBCCBB 3 A_Chase
		Loop
	Melee:
		IIMP DE 6 A_FaceTarget
		IIMP F 6 A_CustomMeleeAttack(random[ImpMeAttack](5,12), "himp/attack", "himp/attack")
		Goto See
	Missile:
		IIMP A 10 A_FaceTarget
		IIMP B 6 A_Skullattack(12)
		IIMP CBAB 6
		Goto Missile+2
	Pain:
		IIMP G 3
		IIMP G 3 A_Pain
		Goto See
	Death:
		IIMP G 4 A_Scream
		IIMP G 0 A_CustomMissile("FrostImpChunk1",24,0,15,0,-10)
		IIMP G 0 A_CustomMissile("FrostImpChunk2",24,0,-15,0,-10)     
		IIMP H 5
		Wait
	XDeath:
		IIMP S 5 A_XScream
		IIMP S 0 A_CustomMissile("FrostImpChunk1",24,0,15,0,-10)
		IIMP S 0 A_CustomMissile("FrostImpChunk2",24,0,-15,0,-10)
		IIMP TU 5
		IIMP V 5 A_Gravity
		IIMP W 5 
		Wait
	Crash:
		IIMP I 7 A_NoBlocking
		IIMP J 7 A_Scream
		IIMP K 7
		IIMP L -1
		Stop
	XCrash:
		IIMP X 7
		IIMP Y 7
		IIMP Z -1
	Raise:
		IIMP HG 5
		Goto See
		Stop  
   }
}     

Actor FrostImpLeader : FrostImp 19284
{
	//$Category Monsters
	//$Title Ice Gargoyle
	Health 80
	-MISSILEMORE
	Obituary "$OB_FROSTIMPL"
	States
	{
	Melee:
	Missile:
		IIMP DE 6 A_FaceTarget
		IIMP F 0 A_CustomMissile("FrostImpShard", 10, 5, 6)
		IIMP F 0 A_CustomMissile("FrostImpShard", 10, -5, -6)
		IIMP F 0 A_CustomMissile("FrostImpShard", 10, 2, 2)
		IIMP F 0 A_CustomMissile("FrostImpShard", 10, -2, -2)
		IIMP F 8 A_CustomMissile("FrostImpShard", 10, 0, 0)
		goto See
	}  
}

Actor FrostImpChunk1
{
	Mass 5
	Radius 4
	Speed 2
	States
	{
	Spawn:
		IIMP M 5
		IIMP NO 700
		Stop
	}
}

Actor FrostImpChunk2
{
	Mass 5
	Speed 2
	Radius 4
	States
	{
	Spawn:
		IIMP P 5
		IIMP QR 700
		Stop
	}
}

Actor FrostImpShard : IceGuyFX2
{
  Gravity 0
  Damage (Random(1, 3))
  Scale 0.5
  SeeSound "FrostImp/Shards"
  DeathSound "IceGuyMissileExplode"
  States
  {
  Spawn:
    IMSD ABC 5 Bright
    Loop
  Death:
    IMSD DEFGH 4 Bright
    Stop
  }
}
