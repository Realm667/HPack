Actor FrostImp : HereticImp 19283
{
	//$Category Monsters
	//$Title Frost Gargoyle
	Obituary "$OB_FROSTIMP"
	HitObituary "$OB_FROSTIMPH"
	Tag "$FN_FROSTIMP"
	BloodColor "A4 A4 B8"
	SeeSound "frostimp/sight"
	PainSound "frostimp/pain"
	DeathSound "frostimp/death"
	AttackSound "frostimp/attack"
	ActiveSound "frostimp/active"
	+NOICEDEATH //How do you freeze an ice cube? ;)
	Scale 0.8
	States
	{
	Spawn:
		IIMP ABCB 10 A_Look
		Loop
	See:
		IIMP AABBCCBB 3 A_Chase
		Loop
	Melee:
		IIMP DE 6 Bright A_FaceTarget
		IIMP F 6 Bright A_CustomMeleeAttack(random[ImpMeAttack](5,12), "frostimp/melee", "frostimp/melee")
		Goto See
	Missile:
		IIMP A 10 A_FaceTarget
		IIMP B 6 A_Skullattack(12)
		IIMP CBAB 6
		Goto Missile+2
	Pain:
		IIMP G 3
		IIMP G 3 A_Pain
		Goto See
	Death:
		IIMP G 4 A_Scream
		IIMP G 0 A_CustomMissile("FrostImpChunk1",24,0,15,0,-10)
		IIMP G 0 A_CustomMissile("FrostImpChunk2",24,0,-15,0,-10)
		IIMP H 5
		Wait
	Crash:
		IIMP I 6 A_NoBlocking
//		IIMP J 6 A_IceGuyDie
		IIMP J 6 A_PlaySound("frostimp/shards")
		IIMP KLM 6
		IIMP N -1
		Stop
	Raise:
		IIMP LKJIHG 4
		Goto See
	}
}

Actor FrostImpLeader : FrostImp 19284
{
	//$Category Monsters
	//$Title Ice Gargoyle
	Health 80
	-MISSILEMORE
	Obituary "$OB_FROSTIMPL"
	States
	{
	Melee:
	Missile:
		IIMP DE 6 Bright A_FaceTarget
		IIMP F 0 Bright A_CustomMissile("FrostImpShard", 24, 5, 6)
		IIMP F 0 Bright A_CustomMissile("FrostImpShard", 24, -5, -6)
		IIMP F 0 Bright A_CustomMissile("FrostImpShard", 24, 2, 2)
		IIMP F 0 Bright A_CustomMissile("FrostImpShard", 24, -2, -2)
		IIMP F 8 Bright A_CustomMissile("FrostImpShard", 24, 0, 0)
		goto See
	}
}

Actor FrostImpChunk1
{
	Mass 5
	Radius 4
	Speed 2
	States
	{
	Spawn:
		IIMP O 700
		Stop
	}
}

Actor FrostImpChunk2
{
	Mass 5
	Speed 2
	Radius 4
	States
	{
	Spawn:
		IIMP Q 700
		Stop
	}
}

Actor FrostImpShard : IceGuyFX2
{
  Gravity 0
  Damage (Random(1, 3))
  Scale 0.5
  SeeSound "FrostImp/Shards"
  DeathSound "misc/icehit"
  States
  {
  Spawn:
    IMSD ABC 5 Bright
    Loop
  Death:
    IMSD DEFGH 4 Bright
    Stop
  }
}
