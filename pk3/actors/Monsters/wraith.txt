ACTOR HPWraith replaces Wraith 30106 
{
	//$Category Monsters
	//$Title Wraith
	Health 50
	Radius 16
	Height 32 
	Mass 50 
	Speed 10
	Damage 2
	RenderStyle Add
	Obituary "$OB_WRAITH"
	DamageType "Wraith"
	DamageFactor "Wraith", 0
	Alpha 0.6
	PainChance 96
	Monster
	+DONTGIB
	+NOTARGET
	+NOGRAVITY
	+FLOAT
	+DONTFALL
	+DONTHURTSPECIES
	+GHOST
	+THRUGHOST
	+FLOATBOB
	+VISIBILITYPULSE
	+NOBLOOD
	AttackSound "HWraith/Attack"
	PainSound "HWraith/Pain" 
	DeathSound "HWraith/Death"
	ActiveSound "HWraith/Active"
	SeeSound "HWraith/Active"
	States 
	{ 
	Spawn: 
		WRAI AB 10 A_Look 
		Loop 
	See: 
		WRAI AABB 3 A_VileChase 
		Loop 
	Missile: 
		WRAI A 4 A_FaceTarget 
		WRAI B 4 A_SkullAttack 
		WRAI AB 4 
		Goto See
	Heal:
		WRAI A 0 A_Die("Suicide")
	Pain: 
		WRAI A 3  
		WRAI B 3 A_Pain 
		Goto See 
	Death: 
		WRAI C 3 A_Noblocking
		WRAI D 3 A_Scream 
		WRAI EFGH 3  
		Stop
	Death.Suicide:
		WRAI AB 3
		WRAI C 3 A_Noblocking
		WRAI D 3 A_Scream 
		WRAI EFGH 3  
		Stop
	FadeIn: // for use with SetActorState.
		WRAI A 0 A_Scream
		WRAI A 0 A_ChangeFlag("NonShootable", 1)
		WRAI A 0 A_ChangeFlag("Shootable", 0)
		WRAI A 0 A_ChangeFlag("NoPain", 1)
		WRAI A 0 A_SetTranslucent(0.0, 1)
		WRAI BBBAAABBB 2 A_FadeIn(0.06, 0)
		WRAI A 0 A_ChangeFlag("NonShootable", 0)
		WRAI A 0 A_ChangeFlag("Shootable", 1)
		WRAI A 0 A_SetTranslucent(0.6, 1)
		WRAI A 0 A_ChangeFlag("NoPain", 0)
		Goto Spawn
	}
}