//Place the RainDropSpawner on your map, and use values set on the specials tab to modify it

//The first argument controls it's area. 128, for example makes it a 256x256 square or 256-radius circle (See fourth argument)
//The second argument controls it's frequency. The lower the number, the heavier the rainfall
//The third argument controls whether or not it plays it's ambient sound. 0 will play the sound, 1 will not
//The fourth argument controls the area type. 0 is a square, 1 is a circle

// The spawner code is originally by Tormentor667 and Ghastly, re-written by Blue Shadow

Actor RainSpawner 10010
{
//$Category HPack_SpecialEffects
//$Title Rain
//$Sprite SPSHH0

//$NotAngled

//$Arg0 Area
//$Arg0Default 128

//$Arg1 Frequency
//$Arg1Tooltip The lower the number, the heavier the rainfall.

//$Arg2 Sound
//$Arg2Type 11
//$Arg2Enum { 0 = "Yes"; 1 = "No"; }

//$Arg3 Area type
//$Arg3Type 11
//$Arg3Enum { 0 = "Square"; 1 = "Circle"; }

  Radius 1
  Height 1
  +NOCLIP
  +CLIENTSIDEONLY
  +SPAWNCEILING
  +NOGRAVITY
  +DONTSPLASH

  const int ARG_AREA = 0;
  const int ARG_FREQ = 1;
  const int ARG_SOUND = 2;
  const int ARG_AREATYPE = 3;

  // If the player is this value (or greater) away from the spawner, the rain will not fall.
  // Note that this value is added to the coverage area (ARG_AREA).
  const float CRP_DISTANCE = 768.0;

  States
  {
  Spawn:
    // If ARG_SOUND is less than 1, play the sound. Otherwise, skip and jump to 
    // the "AreaTypeDecide" state to choose the 'area type'.
    TNT1 A 0 NoDelay A_JumpIf(Args[ARG_SOUND] <= 0, "PlaySound")
    Goto AreaTypeDecide

  PlaySound:
    // Play and loop the rain sound, then fall through to the "AreaTypeDecide" state.
    TNT1 A 0 A_PlaySound("Ambient/Rain", CHAN_7, 1.0, TRUE)

  AreaTypeDecide:
    // If ARG_AREATYPE is greater than zero, set the area type as circle, otherwise set it as
    // square.
    TNT1 A 0 A_JumpIf(Args[ARG_AREATYPE] > 0, "Circle")

  Square:
    TNT1 A 0 A_CheckRange(CRP_DISTANCE + Args[ARG_AREA], 2, TRUE)
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[ARG_AREA], Args[ARG_AREA]), Random(-Args[ARG_AREA], Args[ARG_AREA]), -2, 0, 0, 0, 0, SXF_CLIENTSIDE, Args[ARG_FREQ])
    Loop
    TNT1 A 2
    Loop

  Circle:
    TNT1 A 0 A_CheckRange(CRP_DISTANCE + Args[ARG_AREA], 2, TRUE)
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[ARG_AREA], Args[ARG_AREA]), 0, -2, 0, 0, 0, Random(0, 360), SXF_CLIENTSIDE, Args[ARG_FREQ])
    Loop
    TNT1 A 2
    Loop
  }
}

Actor RainDrop
{
  +MISSILE
  +NOBLOCKMAP
  +DONTSPLASH
  -NOGRAVITY
  Height 2
  Radius 2
  Renderstyle Translucent
  Alpha 0.8
  YScale 1.6
  States
  {
  Spawn:
    RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
    Loop
  Death:
    RNDR BCDEFGH 3 A_FadeOut(0.15)
    Stop
  }
}

/* Original/old spawner code
Actor RainSpawner 10010
{
//$Category HPack_SpecialEffects
  Radius 1
  Height 1
  +NOCLIP
  +CLIENTSIDEONLY
  +SPAWNCEILING
  +NOGRAVITY
  +DONTSPLASH
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(Args[2] > 0, "NoSound")
    TNT1 A 0 A_JumpIf(Args[3] > 0, "Circle")
    TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7", 1)
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
    Loop
  Circle:
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 360), 128, Args[1])
  NoSound:
    TNT1 A 0 A_Jumpif(Args[3] > 0, "NoSoundCircle")
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
    Loop
  NoSoundCircle:
    TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 360), 128, Args[1])
    Loop
  }
}

Actor RainDrop
{
  +MISSILE
  +NOBLOCKMAP
  +DONTSPLASH
  -NOGRAVITY
  Height 2
  Radius 2
  Renderstyle Translucent
  Alpha 0.8
  YScale 1.6
  States
  {
  Spawn:
    RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
    Loop
  Death:
    RNDR BCDEFGH 3 A_FadeOut(0.15)
    Stop
  }
}
*/