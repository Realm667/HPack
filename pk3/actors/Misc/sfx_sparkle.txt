/////////////////////////////
// SPARKLE FOUNTAINS ////////
// for UTNT          ////////
// by Tormentor667   ////////
/////////////////////////////

Actor BaseStar
{
	+MISSILE
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	+DONTSPLASH
	+FORCEXYBILLBOARD
	-SOLID
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.01
	Scale 0.20
	States
	{
	Spawn:
		PTCL AABBCCAA 1 Bright A_FadeIn(0.2)
		PTCL AABBCCAABBCCAABBCC 1 Bright A_FadeOut(0.06)
		Stop
	}
}

Actor BaseStarAccel : TeleGlitter1
{
	+MISSILE
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	+DONTSPLASH
	+FORCEXYBILLBOARD
	-SOLID
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.01
	Scale 0.20
	States
	{
	Spawn:
		PTCL AABB 1 Bright A_FadeIn(0.2)
		PTCL A 0 Bright A_AccTeleGlitter
		PTCL CCAA 1 Bright A_FadeIn(0.2)
		PTCL A 0 Bright A_AccTeleGlitter
		PTCL AABB 1 Bright A_FadeOut(0.06)
		PTCL A 0 Bright A_AccTeleGlitter
		PTCL CCAA 1 Bright A_FadeOut(0.06)
		PTCL A 0 Bright A_AccTeleGlitter
		PTCL BBCC 1 Bright A_FadeOut(0.06)
		PTCL A 0 Bright A_AccTeleGlitter
		PTCL AABB 1 Bright A_FadeOut(0.06)
		PTCL A 0 Bright A_AccTeleGlitter
		PTCL CC 1 Bright A_FadeOut(0.06)
		Stop
	}
}

actor WhiteSparkleSpawner 209
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (White)
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOCLIP
	-SOLID
	states
    {
    Spawn:
		TNT1 A 0
	Init:
		TNT1 A 0 A_JumpIf(Args[2] == 0, "default")
		Goto Active
	Default:
		TNT1 A 0 A_SetArg(2, 4)
	Active:
		TNT1 A 0 A_JumpIf(Args[1], "Circle")
		TNT1 A 0 A_JumpIf(Args[3], "SquareAccel")
		TNT1 A 1 A_SpawnItemEx ("BaseStar",
						random(-Args[0], Args[0]),		// X offset
						random(-Args[0], Args[0]),		// Y offset
						random(0, 2*Args[2]),			// Z offset
						0,								// X velocity
						0,								// Y velocity
						random(1,4),					// Z velocity
						0,								// Angle
						SXF_TRANSFERTRANSLATION|SXF_CLIENTSIDE)
		Loop
	SquareAccel:
		TNT1 A 1 A_SpawnItemEx ("BaseStarAccel",
						random(-Args[0], Args[0]),		// X offset
						random(-Args[0], Args[0]),		// Y offset
						random(0, 2*Args[2]),			// Z offset
						0,								// X velocity
						0,								// Y velocity
						frandom(0.1,2.0),				// Z velocity
						0,								// Angle
						SXF_TRANSFERTRANSLATION|SXF_CLIENTSIDE)
		Loop
	Circle:
		TNT1 A 0 A_JumpIf(Args[3], "CircleAccel")
		TNT1 A 1 A_SpawnItemEx ("BaseStar",
						random(-Args[0], Args[0]),		// X offset
						0,								// Y offset
						random(0, 2*Args[2]),			// Z offset
						0,								// X velocity
						0,								// Y velocity
						random(1,4),					// Z velocity
						Random(0, 360),					// Angle
						SXF_TRANSFERTRANSLATION|SXF_CLIENTSIDE)
		Loop
	CircleAccel:
		TNT1 A 1 A_SpawnItemEx ("BaseStarAccel",
						random(-Args[0], Args[0]),		// X offset
						0,								// Y offset
						random(0, 2*Args[2]),			// Z offset
						0,								// X velocity
						0,								// Y velocity
						random(0.1,2.0),				// Z velocity
						Random(0, 360),					// Angle
						SXF_TRANSFERTRANSLATION|SXF_CLIENTSIDE)
		Loop
	Inactive:
		TNT1 A 1
		Loop
	}    
}

actor BlueSparkleSpawner : WhiteSparkleSpawner 210
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (Blue)
	translation "0:35=185:199"
}

actor GreenSparkleSpawner : WhiteSparkleSpawner 211
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (Green)
	translation "0:35=209:224"
}

actor RedSparkleSpawner : WhiteSparkleSpawner 212
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (Red)
	translation "0:35=145:163"
}

actor YellowSparkleSpawner: WhiteSparkleSpawner 213
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (Yellow)
	translation "0:35=111:136"
}

actor PurpleSparkleSpawner: WhiteSparkleSpawner 214
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (Purple)
	translation "0:35=169:176"
}

actor CyanSparkleSpawner : WhiteSparkleSpawner 215
{
	//$Category HPack_SpecialEffects
	//$Title SparkleSpawner (Cyan)
	translation "0:35=192:208"
}
