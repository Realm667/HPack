// Modify the internal Blood actor to spawn flying blood
actor NashGore_Blood : Blood
{
	+FLOORCLIP
	+DONTSPLASH
	States
	{
	Spawn:
		BLUD C 8
		BLUD B 8 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, 15, 176) // [Nash] it used to use A_SpawnItem, but we're getting rid of that crap
		BLUD A 8
		Stop
	}
}


// Blood sprite that flies outwards and leaves trails behind
actor NashGore_FlyingBlood
{
	Scale 0.75
	Health 1
	Radius 8
	Height 1
	Mass 1
	+CORPSE
	+NOTELEPORT
	+NOBLOCKMAP
	+DONTSPLASH
	+FLOORCLIP
	States
	{
	Spawn:
		TNT1 A 0
		FBLD A 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD A 1 
		FBLD A 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD A 1
		FBLD B 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD B 1
		FBLD B 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD B 1
		FBLD C 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD C 1
		FBLD C 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD C 1
		FBLD D 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD D 1
		FBLD D 1 A_SpawnItem("NashGore_FlyingBloodTrail",0,0,0,1)
		FBLD D 1
		Loop
	Crash:
		TNT1 A 1 A_SpawnItem("NashGore_BloodSpot",0,0,0,1)
		Stop
	}
}

Actor NashGore_BloodSpot
{
	Radius 12
	Height 2
	Mass 1
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	-NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("bloodsplat")
		TNT1 A 0 A_Jump(64,4)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(192,4)
		TNT1 A 0 A_Jump(255,4)
		// make the blood spots stay on the ground. 1500 is the default duration.
		BSPT A 1500
		Stop
		BSPT B 1500
		Stop
		BSPT C 1500
		Stop
		BSPT D 1500
		Stop
	}
}

// Blood trails spawned by FlyingBlood
Actor NashGore_FlyingBloodTrail
{
	Scale 0.70
	Mass 1
	+LOWGRAVITY
	+NOTELEPORT
	+NOBLOCKMAP
	+FLOORCLIP
	+DONTSPLASH
	States
	{
	Spawn:
		BTRL A 4
		BTRL B 4
		BTRL C 4
		BTRL D 4
		Stop
	}
}
