//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
//////////////////////

Actor SnowParticle
{
  +MISSILE
  +NOBLOCKMAP
  +DONTSPLASH
  Radius 1
  Height 1
  Damage 0
  Projectile
  RenderStyle Translucent
  Alpha 0.0
  Scale 0.7
  States
  {
  Spawn:
    SNOW AAAAAAA 2 A_FadeIn(0.1)
    SNOW A 3
    goto Spawn+7
  Death:
    SNOW AAAAAA 1 A_FadeOut(0.2)
    stop
  }
}

Actor SnowSpawner 20012
{
//$Category HPack_SpecialEffects
	+NOBLOCKMAP
	+NOSECTOR
	+NOINTERACTION
	+NOGRAVITY
	+NOCLIP
	+CLIENTSIDEONLY
	+SPAWNCEILING 
	+DONTSPLASH
	-SOLID
	Radius 1
	Height 1
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Args[2] > 0, "Circle")
		TNT1 A 0 A_CheckSight("Unsighted")
		TNT1 A 8 A_SpawnItemEx ("SnowParticle", Random(-Args[0], -Args[0]), Random(-Args[0], -Args[0]), 0, Random(0, 1), Random(0, 1), Random(-1, -3), 0, 128, Args[1])
		Loop
	Circle:
		TNT1 A 0 A_CheckSight("Unsighted")
		TNT1 A 8 A_SpawnItemEx ("SnowParticle", Random(-Args[0], -Args[0]), 0, 0, Random(0, 1), Random(0, 1), Random(-1, -3), Random(0, 360), 128, Args[1]) 
		Loop
	Unsighted:
		TNT1 A 1
		Goto Spawn
	}
}
