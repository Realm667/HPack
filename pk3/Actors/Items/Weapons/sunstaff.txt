// Sun Staff ---------------------------------------------

ACTOR HPSunstaff : Weapon 16453
{
	//$Category Weapons
	//$Title Lightbringer
	+WEAPON.NOAUTOFIRE
	Weapon.SisterWeapon "HPSunstaffPowered"
	Weapon.SelectionOrder 2000
	Weapon.Kickback 0
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 20
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "SunStaffAmmo"
	Weapon.AmmoType2 "SunStaffAmmo"
	Weapon.YAdjust 0
	Obituary "$OB_MPSUNSTAFF"
	Inventory.PickupMessage "$TXT_WPNSUNSTAFF"
	Tag "$TAG_SUNSTAFF"
	States
	{
	Spawn:
		WSUN A -1 light(LWANDAMMO)
		Stop
	Ready:
		SUNS AAABBBCCCDDD 1 BRIGHT A_WeaponReady
		Loop
	Deselect:
		SICL A 0 bright A_StopSoundEx("Weapon")
		SUNS KK 1 bright A_Lower
		SUNS LLMMNNNNNNNNN 1 bright A_Lower
		Loop
	Select:
		SUNS NNNNNNNNNNNMMLL 1 bright A_Raise
		Loop
	Fire:
		SUNS A 0 A_PlaySound("weapons/sunstaffstart")
		SUNS A 0 A_PlayWeaponSound("weapons/sunstaffbeam1")
		SUNS KKII 1 BRIGHT A_FireCustomMissile("SunFake2",0,0,0)
		SUNS E 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS E 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS A 0 A_PlaySoundEx("weapons/sunstaffbeam2","Weapon",1)
		SUNS A 0 A_Refire
		Goto Release
	Hold:
		SUNS E 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS E 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS F 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunBeam",0,1,0)
		SUNS G 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunBeam",0,0,0)
		SUNS H 1 BRIGHT A_FireCustomMissile("SunFake",0,0,0)
		SUNS A 0 A_Refire
	Release:
		SUNS A 0 A_PlayWeaponSound("weapons/sunstaffbeam3")
		SUNS JJ 1 BRIGHT A_FireCustomMissile("SunFake2",0,0,0)
		SUNS KD 2 BRIGHT
		Goto Ready
	AltFire:
		SUNS J 2 BRIGHT A_PlaySoundEx("weapons/sunstaffshoot","Auto",0) 
		SUNS I 2 BRIGHT 
		SUNS N 0 A_PlayWeaponSound("weapons/sunstaffrip") 
		SUNS N 0 A_Recoil(4)
		SUNS N 0 A_LightInverse
		SUNS N 2 Offset (0,48) A_FireCustomMissile("SunSphere", 0, 1, 0, 0)
		SUNS N 2 Offset (0,40) A_Light0
		SUNS N 2 Offset (0,36) A_PlaySound("weapons/sunstaffnew") 
		SUNS N 2 Offset (0,34)
		SUNS N 6 Offset (0,32)
		SUNS ML 3
		SUNS KABCD 3 BRIGHT
		Goto Ready
	}
}

ACTOR HPSunStaffPowered : HPSunStaff
{
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.POWERED_UP
	Tag "$TAG_SUNSTAFF"
	Weapon.SisterWeapon "HPSunStaff"
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 50
	States
	{
	Fire:
	Hold:
		SUNS J 0 A_PlayWeaponSound("weapons/sunstaffrip") 
		SUNS J 3 BRIGHT A_PlaySoundEx("weapons/sunstaffhit","Auto",0)
		SUNS J 0 A_GunFlash
		SUNS I 3 BRIGHT Offset (0,34) A_FireCustomMissile("SunPower", 0, 1, 0, 0)
		SUNS H 3 BRIGHT Offset (0,36)
		SUNS K 3 BRIGHT Offset (0,32)
		SUNS BCD 3 BRIGHT
		SUNS A 0 A_Refire		
		Goto Ready
	Flash:
		TNT1 A 1 A_LightInverse
		TNT1 A 0 A_Light0
		Stop
	AltFire:
		SUNS A 0 A_JumpIfNoAmmo("Ready")
		SUNS A 0 A_PlayWeaponSound("Weapons/sunstaffcharge")
		SUNS BCDABCDABCDABCD 2 BRIGHT 
		SUNS EFGHEFGHEFGH 2 BRIGHT 
		SUNS I 4 BRIGHT 
		SUNS N 0 A_SetBlend("FF FF 88", 1.0, 35)
		SUNS A 0 A_PlayWeaponSound("Weapons/sunstaffpowshoot")
		SUNS N 2 Offset (0,48) A_FireCustomMissile("SunMeltdown", 0, 1, 0, 0)
		SUNS N 2 Offset (0,40)
		SUNS N 2 Offset (0,36)
		SUNS N 2 Offset (0,34)
		SUNS N 10 Offset (0,32) 
		SUNS N 10 A_PlaySound("weapons/sunstaffnew") 
		SUNS ML 3
		SUNS KABCD 3 BRIGHT
		Goto Ready
	}
}

actor SunBeam : FastProjectile
{
	PROJECTILE
	+PAINLESS
	+RIPPER
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	-BLOODSPLATTER
	radius 6
	height 4
	speed 100
	damage (random(1,2))
	alpha 1.0
	scale .9
	renderstyle Add
	seesound ""
	deathsound ""
	Damagetype Solar
	states
	{
	Spawn:
		//SUNP AB 1 BRIGHT Light(SUNSTAFFFX) A_CustomMissile("SunBlur",0,0,180)
		TNT1 A 1 BRIGHT Light(SUNSTAFFFX) A_CustomMissile("SunSpot",0,0,180)
		Wait
	Death:
		SUND ABCDEF 2 bright Light(SUNSTAFFFX) 
		Stop
	}
}

Actor SunFake  : SunBeam { Damage 0 }
Actor SunFake2 : SunFake
{
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT Light(SUNSTAFFFX) A_CustomMissile("SunSpot2",0,0,180)
		Wait
	}
}

Actor SunBlur
{
	height 2
	radius 6
	damage 0
	speed 0.2
	renderstyle Add
	alpha 0.9
	scale 1.5
	+FORCEXYBILLBOARD
	+NOGRAVITY
	+DROPOFF
	states
	{
	Spawn:
		SUNP A 2 BRIGHT Light(SUNSTAFFFX) A_FadeOut(0.2)
		Loop
	}
}

Actor SunSpot : SunBlur
{
	States
	{
	Spawn:
		SUNP BB 1 BRIGHT Light(SUNSTAFFFX) 
		Stop
	}
}

Actor SunSpot2 : SunBlur
{
	states
	{
	Spawn:
		SUNP AA 1 BRIGHT Light(SUNSTAFFFX) 
		Stop
	}
}



actor SunSphere
{
	PROJECTILE
	+FORCEXYBILLBOARD
	radius 6
	height 4
	speed 25
	damage 10
	alpha 1.0
	renderstyle Add
	deathsound "weapons/sunstaffexplode"
	Damagetype Solar
	states
	{
		Spawn:
			SUNM A 0 
			SUNM A 0 A_PlaySoundEx("weapons/sunstaffhit","Auto",0)
			SUNM A 0 A_PlaySoundEx("weapons/sunstaffSphereFly","Voice",1)
			SUNM ABCDEF 3 bright Light(SUNSTAFFFX) 
			Goto Spawn+3
		Death:
			SUNM A 0 A_PlaySoundEx("weapons/sunstaffSphereExp","Auto",0)
			SUNM G 3 bright Light(SUNSTAFFFX) 
			SUNM H 3 bright Light(SUNSTAFFFX) A_Explode(64,128,1)
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 6,  0,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 6, 90,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 6,180,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 6,0,270,32,0) SUNB I 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 5, 15,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 5,105,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 5,195,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 5,0,285,32,0) SUNB I 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 4, 30,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 4,120,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 4,210,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 4,0,300,32,0) SUNB I 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 3, 45,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 3,135,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 3,225,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 3,0,315,32,0) SUNB J 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 2, 60,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 2,150,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 2,240,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 2,0,330,32,0) SUNB J 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 1, 75,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 1,165,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 1,255,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 1,0,345,32,0) SUNB J 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 0, 90,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 0,180,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0, 0,270,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20, 0,0,  0,32,0) SUNB K 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-1,105,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-1,195,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-1,285,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,-1,0, 15,32,0) SUNB K 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-2,120,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-2,210,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-2,300,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,-2,0, 30,32,0) SUNB K 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-3,135,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-3,225,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-3,315,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,-3,0, 45,32,0) SUNB L 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-4,150,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-4,240,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-4,330,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,-4,0, 60,32,0) SUNB L 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-5,165,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-5,255,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-5,345,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,-5,0, 75,32,0) SUNB L 1 bright
			SUNM A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-6,180,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-6,270,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,0,-6,  0,32,0) SUNB A 0 A_SpawnItemEx("SunProj",8,0,12,20,-6,0, 90,32,0) 
			SUNM MNOP 3 bright
			stop
	}
}

actor SunProj
{
	PROJECTILE
	+PAINLESS
	+RIPPER
	+HERETICBOUNCE
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	+NOBOSSRIP
	-BLOODSPLATTER
	radius 16
	height 8
	speed 20
	damage (2*random(1,6))
	alpha 1.0
	scale 1.25
	renderstyle Add
	Damagetype Solar
	states
	{
		Spawn:
			SUNP AB 1 BRIGHT A_CustomMissile("SunBlur",0,0,180)
			Wait
		Death:
			SUND ABCDEF 2 bright Light(SUNSTAFFFX) 
			stop
	}
}

actor SunPower : SunProj
{
	-PAINLESS
	+HEXENBOUNCE
	speed 80
	Damage 10
	Reactiontime 100
	seesound "weapons/sunstaffrip"
	states
	{
		Spawn:
			SUNP A 0
			SUNP A 0 A_PlaySoundEx("weapons/sunstaffbeam2","Body",1)
			SUNP AB 1 BRIGHT A_CustomMissile("SunBlur",0,0,180)
			SUNP A 0 A_Countdown
			Goto Spawn+2
		Death:
			TNT1 A 1 A_StopSoundEx("Body")
			stop
		XDeath:
			TNT1 A 1 A_StopSoundEx("Body")
			SUND ABCDEF 2 bright Light(SUNSTAFFFX) 
			stop
	}
}

ACTOR SunMeltdown
{
	PROJECTILE
	radius 16
	height 8
	speed 0
	Damagetype Solar
	states
	{
		Spawn:
			TNT1 A 0
		Death:
			TNT1 A 1 A_BFGSpray("SunBurst",40,15)
			stop
	}
}

ACTOR SunBurst
{
	Radius 8
	Height 16
	RenderStyle Add
	+NOGRAVITY
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		SUNM G 0 Bright
		SUNM G 0 Bright A_PlaySound("weapons/sunstaffSphereExp")
		SUNM GGGG 0 A_SpawnItemEx("SunRay",0,0,0,8,0,random(4,16),random(1,360),160,0)
		SUNM G 4 Bright Light(SUNSPHERE_X1)
		SUNM HI 4 Bright Light(SUNSPHERE_X2)
		SUNM JK 4 Bright Light(SUNSPHERE_X3)
		SUNM LM 4 Bright Light(SUNSPHERE_X4)
		SUNM NO 4 Bright Light(SUNSPHERE_X5)
		SUNM P 4
		Stop
	}
}

ACTOR SunRay
{
	Radius 5
	Height 6
	Speed 16
	Projectile
	+THRUACTORS
	-NOGRAVITY
	+FORCEXYBILLBOARD
	RenderStyle Add
	States
	{
	Spawn:
		FX01 AAABBB 1 BRIGHT Light(SUNSTAFFFX) A_SpawnItemEx("SunRayTrail",0,0,0,0,0,0,0,0)
		Loop
	Death:
		FX01 EFGH 3 BRIGHT Light(SUNSTAFFFX) 
		Stop
	}
}


ACTOR SunRayTrail
{
  +FORCEXYBILLBOARD
  +NOBLOCKMAP
  Gravity 0.125
  RenderStyle Add
  Alpha 0.4
  States
  {
  Spawn:
    FX01 EFGH 2 BRIGHT
    Stop
  }
}
