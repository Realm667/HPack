// Corvus's Staff -------------------------------------------------------

/*
Staff Combos:

Left-Right : Doubleswipe, Bash.
Left-Left-Left : Doubleswipe, Uppercut, Thrust.
Left-Left-Right-Right : Doubleswipe, Uppercut, Righthook, Overhead.
Left-Left-Right-Left-Right : Doubleswipe, Uppercut, Righthook, Leftswipe, Overhead.

Right-Right : Block, Hold.
Right-Left-Left : Block, Swing, Bash.
Right-Left-Right-Right : Block, Swing, Leftswipe, Thrust.
*/

ACTOR HPStaff : Staff replaces Staff
{
	Tag "$TAG_STAFF"
	Weapon.SelectionOrder 3700
	Weapon.SisterWeapon "HPStaffPowered"
	Weapon.Kickback 10
	Obituary "$OB_MPSTAFF"
	+WEAPON.MELEEWEAPON
	//+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	Decal "BulletChip"
	Inventory.PickupMessage "ELVEN STAFF"
	Inventory.Icon "WSTFI0"
   States
   {
   Spawn:
      WSTF A -1
      Loop
   Ready:
      STFF A 0 A_TakeInventory("StaffToken", 6)
      STFF A 1 A_WeaponReady
      Wait
   Deselect:
      STFF A 1 A_Lower
      Loop
   Select:
      STFF A 1 A_Raise
      Loop
   Fire:
      STFF A 0 A_JumpIfInventory("MasterScrollActive",1,"Combo")
      STFF A 2
      STFF A 2 Offset(0,28)A_PlaySound("weapons/staffswing")
      STFF B 2 Offset(0,32)
      STFF C 2 Offset(0,34) A_StaffAttack(random[StaffAttack](5, 20), "StaffPuff")
      STFF C 2 Offset(0,32)
      STFF B 2 
      STFF A 2 
      STFF A 8 A_ReFire
      Goto Ready
   Combo:
      STFF A 0 A_JumpIfInventory("StaffToken",6,"Right1")
      STFF A 0 A_JumpIfInventory("StaffToken",5,"Left5")
      STFF A 0 A_JumpIfInventory("StaffToken",4,"End")
      STFF A 0 A_JumpIfInventory("StaffToken",3,"Left3")
      STFF A 0 A_JumpIfInventory("StaffToken",2,"Left2")
      STFF A 0 A_JumpIfInventory("StaffToken",1,"Left1")
    Left0: //Doubleswipe
      TNT1 A 0 A_GiveInventory("StaffToken", 1)
      STFF DE 2 
      STFF F 2 A_PlayWeaponSound("weapons/staffswing1")
      STFF G 2 A_CustomPunch(5, 0, 0, "StaffPuff", 120)
      STFF HIJK 1
      STFF L 2 A_PlayWeaponSound("weapons/staffswing3")
      STFF M 2 A_CustomPunch(5, 0, 0, "StaffPuff", 96)
      STFF NO 2
      TNT1 AAAAAAAAAAAAAAAA 1 A_WeaponReady
      goto End
    Left1: //Uppercut
      TNT1 A 4 A_GiveInventory("StaffToken", 1)
      STFF U 2 
      STFF T 1 A_PlayWeaponSound("weapons/staffswing1")
      STFF S 1 A_CustomPunch(7, 0, 0, "StaffPuff", 96)
      STFF R 1 
      STFF QP 2 
      TNT1 AAAAAAAAAAAAAAAA 1 A_WeaponReady
      Goto End
    Left2: //Thrust
      TNT1 A 4 A_TakeInventory("StaffToken", 6)
      STFF A 2 A_PlayWeaponSound("weapons/staffswing2")
      STFF B 2
      STFF C 6 A_CustomPunch(15, 0, 0, "StaffPuff", 96)
      STFF B 2
      STFF A 8
	  Goto Ready
	Left3: //Leftswipe
      TNT1 A 4 A_GiveInventory("StaffToken", 1)
      STFF O 2 
      STFF N 2 A_PlayWeaponSound("weapons/staffswing2")
      STFF M 2 
      STFF L 1 A_CustomPunch(6, 0, 0, "StaffPuff", 120)
      STFF KJIH 1
      STFF G 1 A_PlayWeaponSound("weapons/staffswing3")
      STFF F 2 Offset (-20,32) A_CustomPunch(6, 0, 0, "StaffPuff", 64)
      STFF E 2 Offset (-60,32)
      STFF E 2 Offset (-100,0)
	  TNT1 A 0 Offset (0,32)
      TNT1 AAAAAAAAAAAAAAAA 1 A_WeaponReady
	  Goto End
	Left5: //Swing
      TNT1 A 0 A_GiveInventory("StaffToken", 1)
      STFF JK 2
      STFF L 1 A_PlayWeaponSound("weapons/staffswing1")
      STFF M 1 A_CustomPunch(8, 0, 0, "StaffPuff", 96)
      STFF NO 2
      TNT1 AAAAAAAAAAAAAAAA 1 A_WeaponReady
	  Goto End	
    End:
      STFF A 0 A_TakeInventory("StaffToken", 6)
      STFF A 1 Offset (0,64) 
      STFF A 1 Offset (0,56)
      STFF A 1 Offset (0,48)
      STFF A 1 Offset (0,40)
	  Goto Ready
   AltFire:
      STFF A 0 A_JumpIfInventory("MasterScrollActive",1,1)
	  Goto Ready
      STFF A 0 A_JumpIfInventory("StaffToken",6,"Right6")
      STFF A 0 A_JumpIfInventory("StaffToken",5,"Right5")
      STFF A 0 A_JumpIfInventory("StaffToken",4,"Right3")
      STFF A 0 A_JumpIfInventory("StaffToken",3,"Right3")
      STFF A 0 A_JumpIfInventory("StaffToken",2,"Right2")
      STFF A 0 A_JumpIfInventory("StaffToken",1,"Right1")
	Right0: //Block
      TNT1 A 0 A_GiveInventory("StaffToken", 5)
      STFF F 2
      STFF G 2 A_PlayWeaponSound("weapons/staffswing3") 
      STFF H 2
      STFF IIIII 1 A_SpawnItem("StaffBlock",32)
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
      STFF I 0 A_SpawnItem("StaffBlock",32) STFF I 1 A_WeaponReady
	 BlockEnd:
      STFF H 2 A_TakeInventory("StaffToken",6)
      STFF G 2 Offset(10,32)
      //STFF F 2 Offset(24,32)
      goto Ready
	Right1: //Bash
      TNT1 A 6 A_TakeInventory("StaffToken", 6)
      STFF V 2
      STFF W 1 A_PlayWeaponSound("weapons/staffswing1")
      STFF X 1 
      STFF Y 1 A_CustomPunch(12, 0, 0, "StaffPuff", 120)
      STFF Z 1 
      TNT1 A 12
      goto End
	Right2: //Righthook
      TNT1 A 5 A_GiveInventory("StaffToken", 1)
      STFF E 2 Offset (-100,0)
      STFF E 2 Offset (-60,32) A_PlayWeaponSound("weapons/staffswing1")
      STFF F 1 Offset (-20,32) 
      STFF G 1 Offset (0,32) A_CustomPunch(10, 0, 0, "StaffPuff", 120)
      STFF H 1
      STFF H 2 Offset (60,0) 
      STFF H 2 Offset (120,0) 
      TNT1 A 0 Offset (0,32) 
      TNT1 AAAAAAAAAAAAAAAA 1 A_WeaponReady
      goto End
	Right3: //Overhead
      TNT1 A 6 A_TakeInventory("StaffToken", 6)
      STFF P 2
      STFF Q 2 A_PlayWeaponSound("weapons/staffswing2")
      STFF R 1 
      STFF S 1 A_CustomPunch(20, 0, 0, "StaffPuff", 120)
      STFF TU 1 
      TNT1 A 12
      goto End
	Right5: //Hold
      STFF A 0 A_TakeInventory("StaffToken", 6)
	  STFF A 0 A_PlayWeaponSound("weapons/staffblock")
      STFF I 1 Offset(6,42) A_SpawnItem("StaffBlock",32)
      STFF I 1 Offset(3,37) A_SpawnItem("StaffBlock",32)
      STFF I 1 Offset(0,32) A_SpawnItem("StaffBlock",32)
      STFF IIIIIIIIIIIIIIIIIIIIIIIIIIIIII 1 A_SpawnItem("StaffBlock",32)
	  Goto BlockEnd
	Right6: //LeftswipeJump
      STFF A 0 A_TakeInventory("StaffToken", 3)
	  Goto Left3
   }
}

ACTOR StaffToken : Inventory { Inventory.MaxAmount 6 }

ACTOR StaffBlock
{

	Health 5
	height 44
	radius 24
	Speed  8
	RENDERSTYLE NONE
	+NOBLOOD
	+SHOOTABLE
	+NOGRAVITY
	+REFLECTIVE
	States
	{
		Spawn: 
		TNT1 A 1 
		Stop
		Death: 
		TNT1 A 0
		Stop
	}
}

ACTOR HPStaffPowered : StaffPowered replaces StaffPowered
{
	Tag "$TAG_STAFF"
	Weapon.SisterWeapon "HPStaff"
	States
	{
	Ready:	
		STFP ABC 4 BRIGHT A_WeaponReady
		Loop
	Deselect:
		STFP AAAABBBBCCCC 1 BRIGHT A_Lower
		Loop
	Select:
		STFP AAAABBBBCCCC 1 BRIGHT A_Raise
		Loop
	Fire:
      STFP A 2 BRIGHT
      STFP D 2 BRIGHT Offset(0,48) A_PlaySound("weapons/staffswing")
      STFP D 2 BRIGHT Offset(0,32)
      STFP E 2 BRIGHT Offset(0,34) A_StaffAttack(random[StaffAttack](18, 81), "StaffPuff2")
      STFP E 2 BRIGHT Offset(0,32)
      STFP D 2 BRIGHT Offset(0,30)
      STFP C 2 BRIGHT Offset(0,26)
      STFP A 0 BRIGHT Offset(0,32)
      STFP ABC 3 BRIGHT A_ReFire
      Goto Ready
	}
}

ACTOR HPStaffPuff : StaffPuff replaces StaffPuff { +FORCEXYBILLBOARD }
ACTOR HPStaffPuff2 : StaffPuff2 replaces StaffPuff2 { +FORCEXYBILLBOARD }
