// Hellstaff -------------------------------------------------------

ACTOR HPSkull : SkullRod replaces SkullRod
{
	//$Category Weapons
	//$Title Hellstaff
	+WEAPON.ALT_AMMO_OPTIONAL
	Weapon.SelectionOrder 100
	Weapon.YAdjust 10
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 5
	Weapon.AmmoGive1 50
	Weapon.AmmoType1 "SkullRodAmmo"
	Weapon.AmmoType2 "SkullRodAmmo"
	Weapon.SisterWeapon "HPSkullPowered"
	Inventory.PickupMessage "$TXT_WPNSKULLROD"
	Tag "$TAG_SKULLROD"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Fire:
		HROD B 2 BRIGHT A_FireCustomMissile("HornRodFX1", 0, 1, 0, 0) //A_FireSkullRodPL1
		HROD F 2 BRIGHT 
		HROD B 0 A_ReFire
		Goto Ready
	AltFire:
		HROD A 0 A_JumpIfNoAmmo("Ready")
		HROD G 0 Bright A_PlaySound("weapons/wandcharge")
		HROD G 0 Bright A_PlaySoundEx("weapons/spikeattack","Weapon")
		HROD C 2 Bright Offset (0,32)
		HROD D 3 Bright Offset (0,36)
		HROD E 2 Bright Offset (0,40)
		HROD F 3 Bright Offset (0,44)
		HROD G 4 Bright Offset (0,48)
		HROD G 0 Bright A_FireCustomMissile ("SpikeShot",5,0,0,0)
		HROD G 0 Bright A_FireCustomMissile ("SpikeShot",-5,1,0,0)
		HROD G 0 Bright A_FireCustomMissile ("SpikeShot",0,0,0,0)
		HROD F 2 Bright Offset (0,44)
		HROD E 3 Bright Offset (0,40)
		HROD D 2 Bright Offset (0,36)
		HROD C 2 A_ReFire
		Goto Ready
	}
}

ACTOR HPHornRodFX1 : HornRodFX1 replaces HornRodFX1
{
	Decal "PlasmaScorchLower"
	+FORCEXYBILLBOARD
	Scale .75
	Alpha .80
}
ACTOR HPHornRodFX2 : HornRodFX2 replaces HornRodFX2 { +FORCEXYBILLBOARD Decal "HornyRainMaker" } //heh

ACTOR SpikeShot
{
	Radius 8
	Height 48
	Speed 70
	Damage 3
	Projectile
	RenderStyle Normal
	DamageType "Bone"
	+RIPPER
	+FLOORHUGGER
	+THRUGHOST
	-NOGRAVITY
	DeathSound "weapons/hornrodstab"
	States
	{
	Spawn:
		  NULL A 1 A_CustomMissile("Impaler",0,0,0,4)
		  NULL A 1 A_CustomMissile("SpikeLightSpawner",0,0,0,4)
		  NULL A 0 A_Countdown
		  Loop
	Death:
		  NULL A 2 Bright
		  Stop
	}
}

ACTOR Impaler
{   
	Radius 0
	Height 1
	Damage 0
	Speed 0
	Projectile
	RenderStyle Normal
	DamageType "Bone"
	+FLOORHUGGER
	-NOGRAVITY
	States
	{
	Spawn:
		SPIK ABCDE 3
		SPIK E 0 A_Explode (32,16,0)
		SPIK DCBA 3
		Stop 
	}
}

actor SpikeLightSpawner
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+NOCLIP
	-SOLID
	+CLIENTSIDEONLY
	States
	{
	Spawn:
	Active:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Args[1], "Circle")
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx ("SpikeLight", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 
			random(0, 8), (0.1)*Random(1, 3), 0, (0.1)*Random(1, 10), random(0,360), 129, Args[2])
		TNT1 A 0 A_SpawnItemEx ("SpikeLight", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 
			random(0, 8), (0.1)*Random(1, 3), 0, (0.1)*Random(1, 10), random(0,360), 129, Args[2]) 
		Stop
	Circle:
		TNT1 A 0
		TNT1 A 2 A_SpawnItemEx ("SpikeLight", random(-Args[0], Args[0]), 0, random(0, 8), 
			(0.1)*Random(1, 3), 0, (0.1)*Random(1, 10), Random(0, 360), 129, Args[2]) 
		Loop
	Inactive:
		TNT1 A 1
		Loop
	}     
}

ACTOR SpikeLight
{
	+MISSILE
	+NOBLOCKMAP
	+NOINTERACTION
	+FORCEXYBILLBOARD
	-SOLID
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.01
	Scale 0.06
	Gravity 0.25
	States
	{
	Spawn:
		SKLT AAAA 1 Bright A_FadeIn(0.25)
		TNT1 A 0 A_Jump(80,8)
		TNT1 A 0 A_Jump(80,6)
		TNT1 A 0 A_Jump(80,4)   
		TNT1 A 0 A_Jump(80,2)   
		SKLT AAAAA 8 Bright
		SKLT AAAAAAAAAAAAAAAAAAAA 1 Bright A_FadeOut(0.06)
		Stop
	}
}

ACTOR SkullFX : SpikeLight
{
	Scale .15
	Alpha .05
}

ACTOR HPSkullPowered : SkullRodPowered
{
	+WEAPON.POWERED_UP
	Tag "$TAG_SKULLROD"
	Weapon.SelectionOrder 100
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 10
	Weapon.AmmoGive1 0
	Weapon.AmmoType2 "SkullRodAmmo"
	Weapon.YAdjust 10
	Weapon.SisterWeapon "HPSkull"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Fire:
		HROD C 2 A_PlaySoundEx("weapons/hornrodpowshoot2","Auto",0)
		HROD D 3
		HROD E 2
		HROD F 3
		HROD G 4 A_FireSkullRodPL2
		HROD F 2
		HROD E 3
		HROD D 2
		HROD C 2 A_ReFire
		Goto Ready
	AltFire:
		HROD C 3 Offset (0,36) A_PlaySound("weapons/hornrodpowshoot2")
		HROD D 4 Offset (0,44)
		HROD E 4 Offset (0,52)
		HROD F 4 Offset (0,56) A_PlayWeaponSound("weapons/hornrodspike") 
		HROD G 0 A_FireCustomMissile("SpikeShot2",  15, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",  30, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",  45, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",  60, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",  75, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",  90, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", 105, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", 120, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", 135, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", 150, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", 165, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", 180, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",-165, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",-150, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",-135, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",-120, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2",-105, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", -90, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", -75, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", -60, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", -45, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", -30, 0, 0, 4)
		HROD G 0 A_FireCustomMissile("SpikeShot2", -15, 0, 0, 4)
		HROD G 4 A_FireCustomMissile("SpikeShot2",   0,15, 0, 4)
		HROD F 2 Offset (0,56)
		HROD E 2 Offset (0,52)
		HROD D 2 Offset (0,44)
		HROD C 2 Offset (0,36)
		HROD A 10 Offset (0,32)
		HROD A 0 A_ReFire
		Goto Ready
	}
}

ACTOR SpikeShot2 : SpikeShot
{
	States
	{
	Spawn:
		NULL A 1 A_CStaffMissileSlither
		NULL A 0 A_CustomMissile("Impaler",0,0,0,4)
		NULL A 1 A_CStaffMissileSlither
		NULL A 0 A_CustomMissile("SpikeLightSpawner",0,0,0,4)
		NULL A 0 A_Countdown
		Loop
	}
}
