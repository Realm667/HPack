// Hellstaff -------------------------------------------------------

ACTOR HPSkull : SkullRod replaces SkullRod
{
	//$Category Weapons
	//$Title Hellstaff
	Weapon.SelectionOrder 100
	Weapon.YAdjust 10
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 50
	Weapon.AmmoType1 "SkullRodAmmo"
	Weapon.SisterWeapon "HPSkullPowered"
	Inventory.PickupMessage "$TXT_WPNSKULLROD"
	Tag "$TAG_SKULLROD"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Fire:
		HROD B 2 BRIGHT A_FireCustomMissile("HornRodFX1", 0, 1, 0, 0) //A_FireSkullRodPL1
		HROD F 2 BRIGHT 
		HROD B 0 A_ReFire
		Goto Ready
	}
}

ACTOR HPHornRodFX1 : HornRodFX1 replaces HornRodFX1
{
	Decal "PlasmaScorchLower"
	+FORCEXYBILLBOARD
	Scale .75
	Alpha .80
}
ACTOR HPHornRodFX2 : HornRodFX2 replaces HornRodFX2 { +FORCEXYBILLBOARD Decal "HornyRainMaker" } //heh

ACTOR HPSkullPowered : SkullRodPowered
{
	+WEAPON.POWERED_UP
	Tag "$TAG_SKULLROD"
	Weapon.SelectionOrder 100
	Weapon.AmmoUse1 5
	Weapon.AmmoGive1 0
	Weapon.YAdjust 10
	Weapon.SisterWeapon "HPSkull"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Fire:
		HROD C 2 A_PlaySoundEx("weapons/hornrodpowshoot2","Auto",0)
		HROD D 3
		HROD E 2
		HROD F 3
		HROD G 4 A_FireSkullRodPL2
		HROD F 2
		HROD E 3
		HROD D 2
		HROD C 2 A_ReFire
		Goto Ready
	}
}
