// Force Blast ---------------------------------------------

ACTOR HPForce : Weapon 23456
{
	//$Category Weapons
	//$Title Force Blast Spell
	+NOAUTOFIRE
	Weapon.SisterWeapon "HPForcePowered"
	Weapon.SelectionOrder 1700
	Weapon.AmmoUse1 3
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 30
	Weapon.YAdjust 0
	Weapon.AmmoType1 "BlasterAmmo"
	Weapon.AmmoType2 "BlasterAmmo"
	Weapon.Kickback 200
	Inventory.PickupMessage "$TXT_WPNFORCE"
	Tag "$TAG_FORCEBLAST"
	Obituary "$OB_MPFORCE"
	Decal "RailScorchLower"
	States
	{
	Spawn:
		WFRC ABC 5 Light("FORCEBLAST")
		Loop
	Ready:
		FORC A 1 A_WeaponReady
		Loop
	Deselect:
		FORC A 1 A_Lower
		Loop
	Select:
		FORC A 1 A_Raise
		Loop
	Fire:
		FORC B 4
	Hold:
		FORC CDE 3
		FORC F 0 A_SetBlend("44 88 FF", 0.3, 8)
		FORC F 3 Bright A_PlaySound("array/fire")
		FORC E 0 Bright A_FireCustomMissile("ForceShot1",-8,1,16,-4,0)
		FORC E 0 Bright A_FireCustomMissile("ForceShot1",-4,0,16,-4,0)
		FORC E 0 Bright A_FireCustomMissile("ForceShot1",0,0,16,-4,0)
		FORC E 0 Bright A_FireCustomMissile("ForceShot1",4,0,16,-4,0)
		FORC E 0 Bright A_FireCustomMissile("ForceShot1",8,0,16,-4,0)
		FORC GB 4 Bright
		FORC B 3
		FORC B 3 A_ReFire
		Goto Ready
	AltFire:
		FORC B 4
	AltHold:
		FORC CDE 3
		FORC F 0 A_SetBlend("44 88 FF", 0.25, 5)
		FORC F 3 Bright A_PlaySound("array/fire")
		FORC G 3 Bright A_FireBullets (0, 0, 1, 20, "BlastEffect2")
		FORC B 2 Bright A_Refire
		Goto Ready
	}
}

ACTOR HPForcePowered : HPForce
{
	+WEAPON.POWERED_UP
	+WEAPON.ALT_AMMO_OPTIONAL
	Weapon.SisterWeapon "HPForce"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 5
	Tag "$TAG_FORCEBLAST"
	States
	{
	Fire:
		FORC B 4
	Hold:
		FORC E 3 Bright
		FORC F 0 A_SetBlend("44 88 FF", 0.25, 15)
		FORC F 3 Bright A_PlaySound("array/firep")
		FORC F 5 Bright A_FireCustomMissile("ForceShot2",0,1,8,0,0)
		FORC F 5 Bright A_FireCustomMissile("ForceShot2",0,1,12,-2,0)
		FORC F 5 Bright A_FireCustomMissile("ForceShot2",0,1,16,-4,0)
		FORC G 4
		FORC B 9 
		FORC B 3 A_ReFire
		Goto Ready
	AltFire:
	AltHold:
		FORC B 4 A_JumpIfNoAmmo("Ready")
		FORC E 3
		FORC F 0 A_SetBlend("44 88 FF", 0.4, 5)
		FORC F 0 Bright A_PlaySound("array/fire")
		FORC F 3 Bright A_Blast(BF_USEAMMO, 128, 192, 10, "BlastEffect2", "BlastRadius")
		FORC GB 4 Bright
		FORC B 0 Bright A_Refire
		Goto Ready
	}
}

ACTOR ForceShot1
{
	Radius 3
	Height 3
	Speed 60
	Damage 3
	Alpha .5
	Projectile
	RenderStyle Add
	DeathSound "weapons/skulprojdeath"
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		RADE A 1 Bright Light("FORCEBLAST_X2")
		RADE AEAEAEAEAEAE 2 Bright Light("FORCEBLAST_X2") A_SpawnItemEx("ForceTrail",0,0,0,0,0,0,0,8)
	Death:
		RADE A   2 Bright Light("FORCEBLAST_X2")
		RADE BCD 2 Bright Light("FORCEBLAST_X1")
		RADE EF  2 Bright Light("FORCEBLAST_X2")
		RADE GHI 2 Bright Light("FORCEBLAST_X3")
		Stop
	}
}

ACTOR ForceShot2 : ForceShot1
{
	Radius 6
	Height 6
	Speed 60
	Damage 20
	Alpha .5
	Scale 2
	Projectile
	RenderStyle Add
	DeathSound "array/impactp"
	States
	{
	Spawn:
		RADE A 1 Bright Light("FORCEBLAST_X2")
		RADE AEAEAEAEAEAE 2 Bright Light("FORCEBLAST_X2") A_SpawnItemEx("ForceTrail2",0,0,0,0,0,0,0,8)
	Death:
		// 8-way particles:
		TNT1 A 0 A_PlaySound("array/impactp")
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,-7,-7)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,-7,7)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,7,7)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,7,-7)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,14,0)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,0,14)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,-14,0)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,0,-14)
		// Alternate height 8-way:
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,-5,-5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,-5,5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,5,5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,5,-5,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,10,0,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,0,10,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,-10,0,5)
		RADE A 0 A_SpawnItemEx ("ForceTrail2",0,0,0,0,-10,5)
		Goto Super::Death
	}
}

ACTOR ForceTrail
{   
	Radius 1
	Height 1
	Damage 0
	Speed 0
	Projectile
	RenderStyle Add
	Alpha 0.3
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		RADE FGHI 2 Bright Light("FORCEBLAST_X3")
		Stop 
	}
}

ACTOR ForceTrail2 : ForceTrail
{
	Scale 1.5
	-NOGRAVITY
}