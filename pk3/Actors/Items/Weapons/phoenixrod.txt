// Phoenix Rod -------------------------------------------------------

ACTOR HPPhoenix : PhoenixRod replaces PhoenixRod
{
	//$Category Weapons
	//$Title Phoenix Rod
	+WEAPON.NOAUTOFIRE
	Tag "$TAG_PHOENIXROD"
	Weapon.SelectionOrder 2500
	Weapon.AmmoType2 "PhoenixRodAmmo"
	Weapon.AmmoUse2 1
	Weapon.Sisterweapon "HPPhoenixPowered"
	Obituary "$OB_MPPHOENIXROD"
	States
    {
    Fire:
    	PHNX B 5 BRIGHT A_PlaySoundEx("weapons/phoenixshoot2","Auto",0)
    	PHNX C 0 BRIGHT A_Recoil(4)
    	PHNX C 7 BRIGHT A_FireCustomMissile("PhoenixFX1", 0, 1, 0, 0) //A_FirePhoenixPL1
    	PHNX DB 4 BRIGHT
    	PHNX B 0 BRIGHT A_ReFire
    	Goto Ready
		
		// Alt-fire commented out for now, since it'll be replaced soon.
		// Best not rely on the functionality of this one.
		
		/*
	AltFire:
		PHNX B 3 BRIGHT A_PlaySoundEx("weapons/phoenixshoot3","Auto",0)  
		PHNX B 8 BRIGHT
		PHNX C 0 BRIGHT A_Recoil(4)
		PHNX C 7 BRIGHT A_FireCustomMissile("PhoenixBird", 0, 1, 0, 0) //A_FirePhoenixPL1
		PHNX DBA 5 BRIGHT
		PHNX B 0 BRIGHT A_ReFire
		Goto Ready
		*/
	}
}

ACTOR HPPhoenixPowered : PhoenixRodPowered
{
	+WEAPON.POWERED_UP
	Tag "$TAG_PHOENIXROD"
	Obituary "$OB_MPPHOENIXROD"
	Weapon.SelectionOrder 2500
	Weapon.SisterWeapon "HPPhoenix"
	Weapon.AmmoGive 0
	Weapon.AmmoType2 "PhoenixRodAmmo"
	Weapon.AmmoUse2 2
	States
	{
	Ready:
		PHNX AAAAEEEEFFFFEEEE 1 A_WeaponReady
		Loop
	Fire:
		PHNX B 3 BRIGHT Offset(0,36) A_InitPhoenixPL2
	Hold:
		PHNX C 1 BRIGHT Offset(0,36) A_FirePhoenixPL2
		PHNX B 4 BRIGHT Offset(0,40) A_ReFire
	Powerdown:
		PHNX A 4 BRIGHT Offset(0,36) A_ShutdownPhoenixPL2
		Goto Ready
	AltFire:
		PHNX B 3 BRIGHT A_PlaySoundEx("weapons/phoenixshoot3","Auto",0)  
		PHNX B 8 BRIGHT
		PHNX C 0 BRIGHT A_Recoil(4)
		PHNX C 7 BRIGHT
		PHNX C 0 BRIGHT A_FireCustomMissile("PhoenixBird2", 0, 1, 0)
		PHNX DBA 5 BRIGHT
		PHNX B 0 BRIGHT A_ReFire
		Goto Ready
	}
}

ACTOR HPPhoenixPuff : PhoenixPuff replaces PhoenixPuff { +FORCEXYBILLBOARD }

ACTOR PhoenixBird
{
	Radius 8
	Height 8
	Speed 15
	Damage 6
	RenderStyle Add
	Alpha 0.75
	Scale .75
	//SeeSound "weapons/phoenixpowshoot"
	DeathSound "weapons/phoenixhit"
	PROJECTILE
	+RANDOMIZE
	+FIREDAMAGE
	+NODAMAGETHRUST
	+NOBOSSRIP
	+RIPPER
	ReactionTime 999
	Decal "Scorch"
	States
	{
	Spawn:
		PHOX A 0 Bright
		PHOX A 0 Bright A_PlaySoundEx("weapons/phoenixpowshoot","Voice",1)
		PHOX A 1 Bright Light("PHOENIXSHOT") A_BishopMissileWeave
		PHOX A 0 Bright Light("PHOENIXSHOT") A_BishopMissileWeave
		PHOX A 0 Bright Light("PHOENIXSHOT") A_SpawnItemEx ("PhoenixTrail",Random(-10,10),Random(-4,8),Random(0,15),0,0,0,0,8)
		PHOX A 1 Bright Light("PHOENIXSHOT") A_SpawnItemEx ("PhoenixTrail",Random(-10,10),Random(-4,8),Random(0,15),0,0,0,0,8)
		PHOX A 0 Bright Light("PHOENIXSHOT") A_BishopMissileWeave
		PHOX A 0 Bright Light("PHOENIXSHOT") A_SpawnItemEX ("PhoenixPuff", 0, 0, 0, 0, 5, 0, 0, 32, 0)
		PHOX A 0 Bright Light("PHOENIXSHOT") A_SpawnItemEX ("PhoenixPuff", 0, 0, 0, 0, -5, 0, 0, 32, 0)
		PHOX A 0 Bright Light("PHOENIXSHOT") A_SpawnItemEx ("PhoenixTrail",Random(-10,10),Random(-4,8),Random(0,15),0,0,0,0,8)
		PHOX A 1 Bright Light("PHOENIXSHOT") A_SpawnItemEx ("PhoenixTrail",Random(-10,10),Random(-4,8),Random(0,15),0,0,0,0,8)
		PHOX A 0 Bright Light("PHOENIXSHOT") A_SeekerMissile(5, 20, SMF_LOOK | SMF_PRECISE)
		PHOX A 0 A_CountDown
		Goto Spawn+1
	Death:
		FX08 A 6 Bright Light("PHOENIX_X1")
		FX08 B 6 Bright Light("PHOENIX_X2")
		FX08 C 6 Bright Light("PHOENIX_X3")
		FX08 D 6 Bright Light("PHOENIX_X4")
		FX08 E 6 Bright Light("PHOENIX_X5")
		FX08 FGH 6 Bright Light("PHOENIX_X6")
		stop
	}
}

ACTOR PhoenixBird2 : PhoenixBird
{
	Scale 1
	Damage 8
	ReactionTime 50
	-NODAMAGETHRUST
	+SEEKERMISSILE
	+HEXENBOUNCE
}

ACTOR PhoenixTrail
{   
   Radius 0
   Height 1
   Speed 0
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.80
   States
   {
   Spawn:
      TNT1 A 2
      FX09 GHGH 2 Bright Light("PHOENIXFLAME3")
      FX09 I 2 Bright Light("PHOENIXFLAME3") A_SetTranslucent (0.25,0)
      FX09 JK 2 Bright Light("PHOENIXFLAME3")
      Stop 
   }
}

Actor PhoenixExplosion
{
  renderstyle Add
  Speed 1
  Alpha 0.75
  DeathSound "weapons/phoenixhit"
  +NOBLOCKMAP
  +NOGRAVITY
  +FIREDAMAGE
  States
  {
  Spawn:
  Death:
    FX08 H 2 Bright Light("PHOENIX_X6")
    FX08 E 2 Bright Light("PHOENIX_X5")
    FX08 C 8 Bright Light("PHOENIX_X3")
    FX08 D 8 Bright Light("PHOENIX_X4")
    FX08 E 8 Bright Light("PHOENIX_X5")
    FX08 FGH 8 Bright Light("PHOENIX_X6")
    Stop
  }
}
