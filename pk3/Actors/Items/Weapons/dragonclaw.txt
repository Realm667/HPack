// Dragon Claw -------------------------------------------------------

ACTOR HPBlaster : Blaster replaces Blaster
{
	//$Category Weapons
	//$Title Dragon Claw
	+BLOODSPLATTER
	Weapon.SelectionOrder 400
	Weapon.AmmoGive 30
	Weapon.YAdjust 0
	Weapon.AmmoUse1 1
	Weapon.AmmoType1 "BlasterAmmo"
	Weapon.SisterWeapon "HPBlasterPowered"
	Inventory.PickupMessage "$TXT_WPNBLASTER"
	Tag "$TAG_BLASTER"
	Obituary "$OB_MPBLASTER"
	Decal "RailScorchLower"
	States
	{
	Spawn:
		WBLS A -1
		Stop
	Ready:
		BLSR A 1 A_WeaponReady
		Loop
	Deselect:
		BLSR A 1 A_Lower
		Loop
	Select:
		BLSR A 1 A_Raise
		Loop
	Fire:
		BLSR BC 3 BRIGHT
	Hold:
		BLSR D 2 Bright A_FireBlasterPL1
		BLSR CB 2 Bright
		BLSR A 0 Bright A_Refire
		Goto Ready
	}
}

ACTOR HPBlasterPuff : BlasterPuff replaces BlasterPuff
{
	+FORCEXYBILLBOARD
	States
	{
	Crash:
		FX17 A 4 Bright Light("CLAWPUFF1")
		FX17 B 4 Bright Light("CLAWPUFF2")
		FX17 C 4 Bright Light("CLAWPUFF3")
		FX17 D 4 Bright Light("CLAWPUFF4")
		FX17 E 4 Bright
		Stop
	Spawn:
		FX17 F 4 Bright Light("CLAWPUFF1")
		FX17 G 4 Bright Light("CLAWPUFF2")
		FX17 H 4 Bright Light("CLAWPUFF3")
		FX17 I 4 Bright Light("CLAWPUFF4")
		FX17 JKL 4 Bright
		Stop
	}
}
ACTOR HPBlasterFX1 : BlasterFX1 replaces BlasterFX1 { +FORCEXYBILLBOARD Decal "HImpScorch" }
ACTOR HPBlasterSmoke : BlasterSmoke replaces BlasterSmoke { +FORCEXYBILLBOARD }
ACTOR HPRipper : Ripper replaces Ripper { +FORCEXYBILLBOARD Decal "HImpScorch" }


ACTOR HPBlasterPowered : HPBlaster
{
	+WEAPON.POWERED_UP
	Weapon.AmmoUse 5
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "HPBlaster"
	Tag "$TAG_BLASTER"
	Obituary "$OB_MPBLASTER"
	States
	{
	Fire:
		BLSR BC 0
	Hold:
		BLSR D 3 Bright A_FireCustomMissile("BlasterFX1")
		BLSR CB 4 Bright
		BLSR A 0 A_ReFire
		Goto Ready
	}
}