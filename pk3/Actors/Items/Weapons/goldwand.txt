// Elven Wand ---------------------------------------------

ACTOR HPWand : GoldWand replaces GoldWand
{
	//$Category Weapons
	//$Title Elven Wand
	+BLOODSPLATTER
	+WEAPON.ALT_AMMO_OPTIONAL
	Weapon.SelectionOrder 1900
	Weapon.AmmoGive 25
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 5
	Weapon.AmmoType1 "GoldWandAmmo"
	Weapon.AmmoType2 "GoldWandAmmo"
	Weapon.SisterWeapon "HPWandPowered"
	Weapon.YAdjust 0
	AttackSound "weapons/wandhit"
	Decal "RailScorchLower"
	Inventory.PickupMessage "$TXT_WPNWAND"
	Tag "$TAG_GOLDWAND"
	Obituary "$OB_MPGOLDWAND"
	  
	States
	{
	Spawn:
		WAND A -1
		Stop
	Fire:
		GWND B 3 BRIGHT
		GWND C 5 BRIGHT A_FireGoldWandPL1
		GWND D 3 BRIGHT
		GWND D 0 BRIGHT A_ReFire
		Goto Ready
	AltFire:
		GWND E 0 A_JumpIfNoAmmo("Ready")
		GWND E 3 A_PlayWeaponSound("weapons/wandshoot")
		GWND F 3 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", -6, 0, 0, 4) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", -4, 0, 0, 4) 
		GWND M 3 BRIGHT A_FireCustomMissile("GoldWandFX3", -5, 1, 0, 3) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", 1, 0, 0, 10) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", -1, 0, 0, 10) 
		GWND N 3 BRIGHT A_FireCustomMissile("GoldWandFX3", 0, 0, 0, 9) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", 6, 0, 0, 6) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", 4, 0, 0, 6) 
		GWND O 3 BRIGHT A_FireCustomMissile("GoldWandFX3", 5, 0, 0, 7) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", 9, 0, 0, 1) 
		GWND M 0 BRIGHT A_FireCustomMissile("GoldWandFX4", 11, 0, 0, 1) 
		GWND P 3 BRIGHT A_FireCustomMissile("GoldWandFX3", 10, 0) 
		GWND KA 3
		Goto Ready
	}
}

ACTOR HPGoldWandPuff1 : GoldWandPuff1 replaces GoldWandPuff1 { +FORCEXYBILLBOARD }
ACTOR HPGoldWandPuff2 : GoldWandPuff2 replaces GoldWandPuff2 { +FORCEXYBILLBOARD }
ACTOR HPGoldWandFX1 : GoldWandFX1 replaces GoldWandFX1 { +FORCEXYBILLBOARD Decal "HImpScorch" }
ACTOR HPGoldWandFX2 : GoldWandFX2 replaces GoldWandFX2 { +FORCEXYBILLBOARD Decal "HImpScorch" }

ACTOR GoldWandFX3 : HPGoldWandFX2
{
   Damage 1
   Scale 0.8
   Speed 40
}

ACTOR GoldWandFX4 : HPGoldWandFX2
{
   Scale 0.5
   Speed 20
}

ACTOR HPWandPowered : GoldWandPowered
{
	+WEAPON.POWERED_UP
	+WEAPON.ALT_AMMO_OPTIONAL
	Tag "$TAG_GOLDWAND"
	Weapon.SelectionOrder 1900
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "HPWand"
	Weapon.AmmoUse2 10
	Weapon.AmmoType2 "GoldWandAmmo"
	Weapon.YAdjust 0
	Obituary "$OB_MPGOLDWAND"

	States
	{
	Fire:
		GWND B 3 
		GWND C 4 A_FireGoldWandPL2
		GWND D 3
		GWND D 0 A_ReFire
		Goto Ready
	AltFire:
		GWND E 0 A_JumpIfNoAmmo("Ready")
		GWND E 3 A_PlayWeaponSound("weapons/wandshoot")
		GWND FM 3
		GWND M 0 A_SetArg(0, -165)
	AltFireLoop:
		GWND M 0 A_FireCustomMissile("GoldWandFX5",  args[0], 0, 0, 4)
		GWND M 0 A_SetArg(0, args[0]+15)
		GWND M 0 A_JumpIf((args[0]<180), "AltFireLoop")
		GWND M 0 A_FireCustomMissile("GoldWandFX5",  180, 1, 0, 4)
		GWND N 3 A_SetArg(0, 0)
		GWND OPKA 3
		Goto Ready
	  }  
}

ACTOR GoldWandFX5 : HPGoldWandFX2
{
   Scale 0.5
   Speed 20
   Damage 6
   DeathSound "weapons/wandhit"
   Decal "HImpScorch"
  States
  {
  Spawn:
    FX01 C 1 BRIGHT A_CStaffMissileSlither    FX01 C 0 A_SpawnItemEx("GoldWandFX6",0,0,0,0,0,0,0,0)
    FX01 C 1 BRIGHT A_CStaffMissileSlither    FX01 C 0 A_SpawnItemEx("GoldWandFX6",0,0,0,0,0,0,0,0)
    FX01 C 1 BRIGHT A_CStaffMissileSlither    FX01 C 0 A_SpawnItemEx("GoldWandFX6",0,0,0,0,0,0,0,0)
    FX01 D 1 BRIGHT A_CStaffMissileSlither    FX01 D 0 A_SpawnItemEx("GoldWandFX6",0,0,0,0,0,0,0,0)
    FX01 D 1 BRIGHT A_CStaffMissileSlither    FX01 D 0 A_SpawnItemEx("GoldWandFX6",0,0,0,0,0,0,0,0)
    FX01 D 1 BRIGHT A_CStaffMissileSlither    FX01 D 0 A_SpawnItemEx("GoldWandFX6",0,0,0,0,0,0,0,0)
    Loop
  }
}

ACTOR GoldWandFX6
{
  +FORCEXYBILLBOARD
  +NOBLOCKMAP
  Gravity 0.125
  RenderStyle Add
  Alpha 0.4
  States
  {
  Spawn:
    FX01 EFGH 2 BRIGHT
    Stop
  }
}
