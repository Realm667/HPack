// Firemace ---------------------------------------------

ACTOR HPMace : Mace replaces MaceSpawner
{
	//$Category Weapons
	//$Title Firemace
	+WEAPON.ALT_AMMO_OPTIONAL
	Tag "$TAG_MACE"
	Weapon.SelectionOrder 1300
	Weapon.SisterWeapon "HPMacePowered"
	Weapon.SlotPriority 0
	Weapon.AmmoType2 "MaceAmmo"
	Weapon.AmmoUse2 3
	Obituary "$OB_MPMACE"
	States
	{
	AltFire:
		MACE B 4 A_JumpIfNoAmmo("Ready")
	AltHold:
		MACE F 0 A_JumpIfNoAmmo("Ready")
		MACE F 3 A_FireCustomMissile(MaceFX5, (frandom(-4.5, 4.5)), true)
		MACE C 0 A_JumpIfNoAmmo(9)
		MACE E 3 A_FireCustomMissile(MaceFX5, (frandom(-4.5, 4.5)), false)
		MACE C 0 A_JumpIfNoAmmo(8)
		MACE D 3 A_FireCustomMissile(MaceFX5, (frandom(-4.5, 4.5)), true)
		MACE C 0 A_JumpIfNoAmmo(7)
		MACE C 3 A_FireCustomMissile(MaceFX5, (frandom(-4.5, 4.5)), false)
		MACE C 0 A_JumpIfNoAmmo(2)
		MACE C 0 A_ReFire
		MACE FEDCB 3
		Goto Ready
	}
}

ACTOR HPMacePowered : MacePowered
{
	+WEAPON.ALT_AMMO_OPTIONAL
	Tag "$TAG_MACE"
	Weapon.SelectionOrder 1300
	Weapon.AmmoType2 "MaceAmmo"
	Weapon.AmmoUse2 10
	Weapon.SisterWeapon "HPMace"
	Obituary "$OB_MPMACE"
	States
	{
	AltFire:
	AltHold:	
		MACE B 0 A_JumpIfNoAmmo("Ready")
		MACE A 0 A_PlaySound("weapons/maceshoot", CHAN_WEAPON)
		MACE F 4 A_FireCustomMissile(MaceFX6, (frandom(-4.5, 4.5)), true)
		MACE BA 4
		MACE B 0 A_JumpIfNoAmmo("Ready")
		MACE D 0 A_ReFire
		Goto Ready
	}
}

ACTOR HPMaceFX1 : MaceFX1 replaces MaceFX1 { +FORCEXYBILLBOARD Decal "BaronScorch" } 
ACTOR HPMaceFX2 : MaceFX2 replaces MaceFX2 { +FORCEXYBILLBOARD } 
ACTOR HPMaceFX3 : MaceFX3 replaces MaceFX3 { +FORCEXYBILLBOARD } 
ACTOR HPMaceFX4 : MaceFX4 replaces MaceFX4 { +FORCEXYBILLBOARD Decal "BFGScorch" } 

ACTOR MaceFX5 : HPMaceFX1
{
	BounceType "Grenade"
	SeeSound "weapons/maceshoot"
	BounceSound "weapons/macebounce"
	BounceFactor 0.75
	Decal "BaronScorch"
	States
	{
	Spawn:
		// This roughly mimics A_MacePL1Check which we cannot use here
		FX02 ABABA 4
		FX02 B 0 A_ChangeFlag(NOGRAVITY, 0)
		FX02 B 0 A_SetGravity(0.125)
		// This prevents them from staying around for too long
		FX02 B 0 A_SetArg(0, 24)
	Idle:
		FX02 AB 4 A_CountdownArg(0)
		Loop
	Death:
		FX02 FGHIJ 4 Bright
		Stop
	}
}

ACTOR MaceFX6 : HPMaceFX4
{
	Speed 12
	Damage 8
	Decal "BFGScorch"
	States
	{
	Death:
		FX02 C 0 A_PlaySound("weapons/maceexplode", CHAN_BODY)
		FX02 C 4 Bright A_Mushroom("MaceFX5", 16, MSF_Classic|MSF_DontHurt, 2.5, 0.75)
		FX02 GHIJ 4 Bright
		Stop
	}
}
