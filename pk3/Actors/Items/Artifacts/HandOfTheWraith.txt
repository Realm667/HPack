// ------------------ //
// HAND OF THE WRAITH //
// ------------------ //

Actor ArtiWraithHand : CustomInventory 18796
{
	//$Category Artifacts
	//$Title Hand of the Wraith
  Inventory.PickupSound "Misc/P_pkup"
  Inventory.PickupMessage "$TXT_ARTIWRAITHHAND"
  Tag "$TAG_ARTIWRAITHHAND"
  Inventory.DefMaxAmount
  Inventory.Icon "HWTHA0"
  +Inventory.InvBar
  +FloatBob
  +Inventory.PickupFlash
  +CountItem
  States
  {
  Spawn:
    HWTH BCD 4 Bright Light(WRAITHHAND)
    Loop
  Use:
    TNT1 A 1 A_FireCustomMissile("WraithHandProjectile", 0, 0, 0)
    Stop
  }
}

Actor WraithHandProjectile
{
  Speed 20
  Damage 0
  Projectile
  +ForcePain
  +ThruGhost
  RenderStyle Add
  Alpha 0.6
  Scale 0.65
  DamageType "HandOfTheWraith"
  SeeSound "HandOfTheWraith/Fire"
  DeathSound "CurseActive"
  States
  {
  Spawn:
    HWTP A 0 Bright 
    HWTP A 0 Bright A_PlaySound("CurseHit", CHAN_AUTO)
    HWTP A 1 Bright A_SpawnItemEx("WraithHandTrail", 0, 0, 0, 0, 0, 0, 0, 128)
    Wait
  Death:
    HWTP GHIJK 3 Bright
    Stop
  }
}

Actor WraithHandTrail
{
  Scale 0.65
  RenderStyle Add
  Alpha 0.6
  +NoGravity
  States
  {
  Spawn:
    TNT1 A 2
    HWTP BCDE 2 Bright
    Stop
  }
}

Actor PowerWraithHand : PowerProtection
{
  DamageFactor "Normal", 2.0 //Add any applicable damagetypes
  DamageFactor "Melee", 2.0
  DamageFactor "Fire", 2.0
  DamageFactor "Ice", 2.0
}


Actor WraithHandDamage : PowerupGiver 
{
  +Inventory.AutoActivate
  Powerup.Type WraithHand
  Inventory.MaxAmount 0
  Powerup.Duration 1000
  States
  {
  Spawn:
    TNT1 A 1
    Fail
  }
}

Actor WraithHandShadow : PowerupGiver 
{
  +Inventory.AutoActivate
  Powerup.Type Ghost
  Inventory.MaxAmount 0
  Powerup.Duration 1000
  States
  {
  Spawn:
    TNT1 A 1
    Fail
  }
}

// -------- //
// END HAND //
// -------- //
