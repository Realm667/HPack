actor HPSentinel : HPIronLich replaces Sentinel 30122
{
	//$Category Monsters
	//$Title Sentinel
	Health 1250
	Radius 31
	Height 72
	CameraHeight 57
	Mass 1000
	DropItem "PhoenixRodAmmo" 84 5
	DropItem "ArtiEgg" 51
	Speed 8
	PainChance 64
	Monster
	+DONTHARMSPECIES
	+FLOAT
	+NOGRAVITY
	+NOTARGET
	+NOBLOOD
	-DONTFALL
	+DONTMORPH
	+DONTSQUASH
	+NOTELEOTHER
	Species "IronLich"
	SeeSound "monster/sensit"
	PainSound "monster/senpai"
	DeathSound "monster/sendth"
	ActiveSound "monster/senact"
	Obituary "$OB_SENTINEL"
	States
	{
	Spawn:
		SENT A 10 A_Look
		Loop
	See:
		SENT A 3 A_Chase
		loop
	Missile:
		SENT A 0 A_Jump(96,2)
		SENT A 0 A_Jump(96,11)
		SENT A 0 A_Jump(96,15)
		SENT A 8 A_FaceTarget
		SENT C 0 Bright A_CustomMissile("SentGMissile",52,12,0,1,0)
		SENT C 8 Bright A_CustomMissile("SentGMissile",52,-12,0,1,0)
		SENT A 6 A_FaceTarget
		SENT C 0 Bright A_CustomMissile("SentGMissile",52,12,0,1,0)
		SENT C 8 Bright A_CustomMissile("SentGMissile",52,-12,0,1,0)
		SENT A 8 A_FaceTarget
		SENT C 0 Bright A_CustomMissile("SentGMissile",52,12,0,1,0)
		SENT C 8 Bright A_CustomMissile("SentGMissile",52,-12,0,1,0)
		Goto See
		SENT D 4 A_FaceTarget
		SENT E 0 Bright A_CustomMissile("CubeShot",52,12,0,1,0)
		SENT E 4 Bright A_CustomMissile("CubeShot",52,-12,0,1,0)
		SENT D 1 A_SpidRefire
		Goto Missile+13
		SENT D 4 A_FaceTarget 
		SENT E 5 Bright A_FaceTarget
		SENT F 8 Bright A_CustomMissile("SentRMissile",24,0,0,0)
		Goto See
	Pain:
		SENT B 3
		SENT B 3 A_Pain
		SENT B 4
		SENT B 2 A_Jump(128,4)
		SENT B 0 A_FaceTarget
		SENT B 0 Bright A_CustomMissile("GCSpawner",24,-5,-30,0)
		SENT B 0 Bright A_CustomMissile("GCSpawner",24,5,30,0)
		SENT B 1
		Goto See
	Death:
		SENT G 8 Bright
		SENT H 7 Bright A_Scream
		SENT IJ 7 Bright
		SENT K 7 A_NoBlocking
		SENT K -1 A_SetFloorClip
		Stop
	Raise:
		SENT KJIHG 8
		Goto See
	}
}

ACTOR SentGMissile
{
   Radius 5
   Height 5
   Speed 16
   FastSpeed 22
   Damage 6
   RenderStyle ADD
   Alpha 0.75
   Projectile
   +THRUGHOST
   Seesound "weapons/boltfi"
   DeathSound "monster/disht1"   
   States
   {
   Spawn:
      CFX6 A 1 Bright A_SpawnItem("Barbtrail",0,0)
      loop
   Death:
      CFX6 BCDE 4 Bright
      Stop
   }
}

ACTOR SentRmissile
{
	Radius 5
	Height 5
	Speed 20
	Damage 15
	DamageType fire
	ExplosionDamage 32
	ExplosionRadius 64
	RenderStyle Add
	Alpha 0.85
	Projectile
	+THRUGHOST
	SeeSound "weapons/bigbrn"
	DeathSound "weapons/firex3"   
	States
	{
	Spawn:
		SEF1 AABBCC 1 Bright A_SpawnItem("RedPuff",0,0)
		Loop
	Death:
		SEF1 DEFG 5 Bright A_Explode
		SEF1 H 5 Bright
		Stop
	}
}

ACTOR GCSpawner
{
	Radius 5
	Height 5
	Speed 12
	RenderStyle Add
	Alpha 0.75
	Projectile
	+THRUGHOST
	-NOGRAVITY
	+LOWGRAVITY
	SeeSound "weapons/smlbrn"
	DeathSound "vile/firecrkl"   
	States
	{
	Spawn:
		SEF2 AABBCCDD 1 Bright A_SpawnItem("Barbtrail",0,0)
		Loop
	Death:
		FIRG A 3 Bright
		FIRG B 3 Bright A_SpawnItem("Guardian",0,0)
		FIRG CDEFGH 3 Bright
		Stop
	}
}

/* // Already defined in ghoul.txt
ACTOR Barbtrail
{   
	Radius 0
	Height 1
	Projectile
	RenderStyle Add
	Alpha 0.75
	States
	{
	Spawn:
		NULL A 1 Bright
		SSFX ABCDEFGH 2 Bright
		Stop 
	}
}
*/

/* //Already defined in Guardian Cube.
ACTOR CubePuff
{   
	Radius 2
	Height 2
	Speed 0
	Scale 1.0
	Projectile
	RenderStyle Add
	Alpha 0.67
	States
	{
	Spawn:
		CBAL HIJ 3 Bright
		Stop 
	
	}
}*/

// No longer defined in deathknight.txt
ACTOR RedPuff
{   
	Radius 0
	Height 1
	Speed 0
	Projectile
	RenderStyle Add
	Alpha 0.85
	States
	{
	Spawn:
		TNT1 A 3 Bright
		RPUF ABCDE 3 Bright
		Stop 
	}
}
