actor ArchWizard : Heresiarch 16516
{
	//$Category Monsters
	//$Title Arch Wizard
	Obituary "$OB_ARCHWIZARD"
	Health 3500
	PainChance 32
	Speed 14
	Radius 40
	Height 88
	Mass 0x7fffffff
	Damage 0
	SeeSound "archwizard/sight"
	DeathSound "wizard/death"
	ActiveSound "archwizard/active"
	PainSound "archwizard/pain"
	BloodType "WizardPurpleBlood"
	+NoBloodDecals
	+MISSILEMORE
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	+NORADIUSDMG
	States
	{
	Spawn:
		ARCM A 3
		ARCM A 2 A_SorcSpinBalls
	Idle:
		ARCM A 10 A_Look
		Wait
	See:
		ARCM AABB 6 A_Chase
		ARCM A 0 A_Jump (16, "Teleport")
		Loop
	Pain:
		ARCM A 4 A_Pain
	Teleport:
		ARCM A 0 A_UnSetshootable
		ARCM A 0 A_FaceTarget
		ARCM A 1 A_PlaySound("misc/teleport")
		ARCM A 1 A_SetTranslucent(0.90)
		ARCM A 1 A_SetTranslucent(0.80)
		ARCM A 1 A_SetTranslucent(0.70)
		ARCM A 1 A_SetTranslucent(0.60)
		ARCM A 1 A_SetTranslucent(0.50)
		ARCM A 1 A_SetTranslucent(0.40)
		ARCM A 1 A_SetTranslucent(0.30)
		ARCM A 1 A_SetTranslucent(0.20)
		ARCM A 1 A_SetTranslucent(0.10)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		ARCM A 1 A_PlaySound("misc/teleport")
		ARCM A 1 A_SetTranslucent(0.10)
		ARCM A 1 A_SetTranslucent(0.20)
		ARCM A 1 A_SetTranslucent(0.30)
		ARCM A 1 A_SetTranslucent(0.40)
		ARCM A 1 A_SetTranslucent(0.50)
		ARCM A 1 A_SetTranslucent(0.60)
		ARCM A 1 A_SetTranslucent(0.70)
		ARCM A 1 A_SetTranslucent(0.80)
		ARCM A 1 A_SetTranslucent(0.90)
		ARCM A 1 A_SetTranslucent(1.0)
		ARCM A 0 A_SetShootable
		ARCM A 0 A_FaceTarget
		Goto See
	Missile:
		ARCM D 0 A_Jump (255, "Fire1", "Fire2", "Fire3", "Fire4")
		Stop
	Fire1:
		ARCM DDDD 4 A_FaceTarget
		ARCM C 8 A_CustomMissile ("ArchWHomingBall", 48, 0, 0, 0)
		ARCM D 8 A_FaceTarget
		Goto See
	Fire2:
		ARCM DDDD 4 A_FaceTarget
		ARCM C 8 A_CustomMissile ("ArchWHomingBall", 48, 0, 0, 0)
		ARCM D 4 A_FaceTarget
		ARCM C 8 A_CustomMissile ("ArchWHomingBall", 48, 0, 0, 0)
		ARCM D 4 A_FaceTarget
		ARCM C 8 A_CustomMissile ("ArchWHomingBall", 48, 0, 0, 0)
		ARCM D 4 A_FaceTarget
		Goto See
	Fire3:
		ARCM DDDDDDDD 4 A_FaceTarget
		ARCM C 8 A_CustomMissile ("ArchWMorphBall", 48, 0, random (-5, 5), 0)
		ARCM D 8 A_FaceTarget
		Goto See
	Fire4:
		ARCM DDDD 4 A_FaceTarget
		ARCM C 8 
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, -6, 0)
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, 0, 0)
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, 6, 0)
		ARCM D 4 A_FaceTarget
		ARCM C 8 
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, -6, 0)
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, 0, 0)
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, 6, 0)
		ARCM D 4 A_FaceTarget
		ARCM C 8 
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, -6, 0)
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, 0, 0)
		ARCM C 0 A_CustomMissile ("ArchWFX1", 48, 0, 6, 0)
		ARCM D 4 A_FaceTarget
		Goto See
	Death: 
		ARCM E 4 A_SpawnItem ("ArchWSkeleton")
		ARCM F 4 A_Scream
		ARCM G 4
		ARCM H 4 A_NoBlocking
		ARCM IJ 4
		ARCM K -1
		Stop
	}
}

ACTOR ArchWSkeleton
{
  +NOINTERACTION
  States
  {
  Spawn:
    SKLD A 32
    SKLD BCDEFGH 3
    SKLD I -1
    stop
  }
}

ACTOR ArchWHomingBall : MageStaffFX2
{
  Speed 15
  SeeSound "HomingBall/Fire"
  DeathSound "HomingBall/Explode"
  DamageType "none"

  States
  {
  Spawn:
    BSBS A 0 Bright Light(DISCIPLEBALL)
    BSBS A 0 Bright Light(DISCIPLEBALL) A_PlaySoundEx ("HomingBall/Fly", "SoundSlot6", 1) 
    BSBS A 2 Bright Light(DISCIPLEBALL) A_MStaffTrack
    BSBS A 0 Bright Light(DISCIPLEBALL) A_SpawnItemEx (ArchWSpark, random (-6, 6), random (-6, 6), random (-6, 6), 0, 0, 0, 0, 160)
    BSBS B 2 Bright Light(DISCIPLEBALL) A_MStaffTrack
    BSBS B 0 Bright Light(DISCIPLEBALL) A_SpawnItemEx (ArchWSpark, random (-6, 6), random (-6, 6), random (-6, 6), 0, 0, 0, 0, 160)
    BSBS C 2 Bright Light(DISCIPLEBALL) A_MStaffTrack
    BSBS C 0 Bright Light(DISCIPLEBALL) A_SpawnItemEx (ArchWSpark, random (-6, 6), random (-6, 6), random (-6, 6), 0, 0, 0, 0, 160)
    BSBS D 2 Bright Light(DISCIPLEBALL) A_MStaffTrack
    BSBS D 0 Bright Light(DISCIPLEBALL) A_SpawnItemEx (ArchWSpark, random (-6, 6), random (-6, 6), random (-6, 6), 0, 0, 0, 0, 160)
    Loop
  Death:
    BSBS E 0 Bright Light(DISCIPLEBALL) A_StopSoundEx ("SoundSlot6")
    BSBS E 0 Bright Light(DISCIPLEBALL) A_SetTranslucent (1, 1) 
    BSBS E 3 Bright Light(DISCIPLEBALL) A_Explode (25, 96, 0)
    BSBS FGHIJKLM 3 Bright
    Stop
  }
}

ACTOR ArchWMorphBall : EggFX
{
  speed 25
  renderstyle Add
  seesound "ArchWizard/Fire"
  
  States
  {
  Spawn:
    SBS4 ABC 2 Bright Light(DISCIPLEBALL) A_SpawnItemEx (ArchWSpark, random (-6, 6), random (-6, 6), random (-6, 6), 0, 0, 0, 0, 160)
    Loop
  Death:
    SBFX IJKL 3 Bright
    Stop
  }
}

ACTOR ArchWFX1 : WizardFX1
{
  speed 25
  damage 8
  seesound "ArchWizard/Fire"
}

actor ArchWSpark
{
  +NOINTERACTION
  RenderStyle Add
  States
  {
  Spawn:
    FX11 G 1 Bright Light(DISCIPLEBALL_X2) A_FadeOut (0.1)
    loop
  }
}
