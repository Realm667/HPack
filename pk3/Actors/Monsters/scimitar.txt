ACTOR Scimitar 12645 
{
	//$Category Monsters
	//$Title Flying Scimitar
   Health 50
   PainChance 85
   Speed 20 
   Radius 16 
   Height 32 
   Mass 25
   Alpha .5
   MeleeDamage 2
   MONSTER
   +FLOORCLIP
   +DONTHURTSPECIES
   +NOGRAVITY
   +FLOAT
   +NOBLOOD
   SeeSound "scimitar/scimisee"
   PainSound "scimitar/scimipai"
   DeathSound "scimitar/scimidth"
   Obituary "$OB_SCIMITAR"

   States 
   { 
   Spawn: 
       SCIM A 3 BRIGHT A_Look 
       Loop 
   See: 
       SCIM A 3 BRIGHT A_Chase 
       Loop 
   Missile:
       SCIM D 4 BRIGHT A_FaceTarget
       SCIM D 4 Bright A_CustomMissile ("ScimShot1",32,0,0,0,0)
       SCIM E 0 Bright A_PlaySound("scimitar/scimiatk")
       Goto See  
   Melee:
       SCIM C 4 BRIGHT A_FaceTarget
       SCIM A 4 BRIGHT
       SCIM B 4 BRIGHT A_MeleeAttack
       SCIM B 0 A_PlaySound ("scimitar/scimihit")
       Goto See
   Pain:  
       SCIM F 6 BRIGHT A_Pain 
       Goto See 
   Death: 
       SCIM G 6 BRIGHT A_Scream
       SCIM H 6 BRIGHT A_NoBlocking
       SCIM I 6 BRIGHT
       SCIM J 6 BRIGHT
       SCIM K 6 BRIGHT
       SCIM L -1
       Stop
   Charge:
       SCIM F 4 A_FaceTarget
       SCIM F 4 A_SkullAttack
       SCIM F 4 A_FaceTarget
       SCIM F 4 A_SkullAttack
       SCIM F 4 A_FaceTarget
       SCIM F 4 A_SkullAttack
       SCIM F 0 A_Stop
       SCIM F 1
       Goto See
   Raise: 
       SCIM BCA 5 
       Goto See 
   } 
}


ACTOR ScimShot1
{
   Radius 4
   Height 8
   Speed 50
   Damage 2
   PROJECTILE
   Damagetype "Bone"
   +THRUGHOST
   +RIPPER
   DeathSound "weapons/glahit"
   States
   {
   Spawn:
      RFX1 PPPPPPPP 1 Bright A_SpawnItemEx("ScimTrail1",0,0,0,0,0,0,0,128,0)
      NULL AAAAAAAA 0 A_SpawnItemEx("ScimShot2",0,0,0,25,random(-6,6),momz+random(-3,3),0,0,0)
      stop
   Death:
      RFX1 L 0 Bright A_SetTranslucent(0.67,1)
      RFX1 LMNO 4 Bright
      stop
   }
}

ACTOR ScimTrail1
{   
   Radius 0
   Height 1
   Damage 0
   Speed 0
   Scale 0.5
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.50
   States
   {
   Spawn:
      BONE BCDEFGH 2 Bright
      Stop 
   }
}

ACTOR ScimShot2
{
   Radius 2
   Height 4
   Speed 35
   Damage 1
   PROJECTILE
   Damagetype "Bone"
   +THRUGHOST
   DeathSound "weapons/thornx"
   States
   {
   Spawn:
      RFX1 A 1 Bright A_SpawnItemEx("ScimTrail2",0,0,0,0,0,0,0,128,0)
      loop
   Death:
      RFX1 G 0 Bright A_SetTranslucent(0.67,1)
      RFX1 GHIJCDEF 3 Bright
      stop
   }
}

ACTOR ScimTrail2
{   
   Radius 1
   Height 1
   RENDERSTYLE ADD
   ALPHA 0.50
   +NOGRAVITY
   +NOBLOCKMAP
   States
   {
   Spawn:
      RFX1 BCDEF 3 Bright
      Stop 
   }
}
