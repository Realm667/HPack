actor Satyr 3999
{
	//$Category Monsters
	//$Title Satyr Archer
    Health 150
    Radius 24
    Height 64
    Scale 1
    Speed 13
    PainChance 50
    Mass 150
    MONSTER 
    +FLOORCLIP
    SeeSound "satyr/sight"
    PainSound "satyr/pain"
    DeathSound "satyr/death"
    ActiveSound "knight/active"
    Obituary "$OB_SATYR"
    MissileType "SatyrArrow"
    DropItem "CrossbowAmmo2", 64, 5
    States
    {
    Spawn:
	   STYR AB 10 A_Look
	   Loop
    See:
	   STYR AABBCCDD 3 A_Chase
	   Loop
    Missile:
       STYR E 6 A_FaceTarget
       STYR F 6
       STYR G 10
       STYR H 0 A_MissileAttack
       STYR I 4
       Goto See
    Pain:
       STYR A 2
       STYR A 2 A_Pain
       Goto See
	Pain.HandOfTheWraith:
	   STYR A 0 A_GiveInventory("WraithHandShadow", 1) //+Ghost flag and translucency
	   STYR A 0 A_GiveInventory("WraithHandDamage") //Vulnerability
	   STYR A 2
	   STYR A 2 A_Pain
	   Goto See
    Death:
       STYR J 5 A_CustomMissile("SatyrBow",44,32,-90,0)
       STYR K 5 A_Scream
       STYR L 6
       STYR M 7 A_Fall
       STYR N 4
       STYR O 4
       STYR P -1
       Stop
    Ice:
       STYR Q 0 A_CustomMissile("SatyrBow",44,32,-90,0)
       STYR Q 5 A_FreezeDeath
       STYR Q 1 A_FreezeDeathChunks
       wait
    Raise:
        STYR PONMLKJ 8
        Goto See
    }
}

ACTOR SatyrArrow
{
   Radius 3
   Height 3
   Speed 40
   Damage 3
   Scale .75
   PROJECTILE
   //-NOGRAVITY
   RENDERSTYLE Normal
   Damagetype "Bone"
   +THRUGHOST
   DeathSound "satyr/hit"
   States
   {
   Spawn:
      RFX1 P 1 Bright
      RFX1 P 0 A_SpawnItemEx("SatyrTrail",0,0,0,0,0,0,0,8)
      loop
   Death:
      TNT1 A 1 A_SpawnItemEx("SatyrLightSpawner")
      TNT1 A 0 A_SpawnItemEx("SatyrLightSpawner")
      TNT1 A 0 A_SpawnItemEx("SatyrLightSpawner")
      TNT1 A 0 A_SpawnItemEx("SatyrLightSpawner")
      stop
   }
}

ACTOR SatyrTrail
{   
   Radius 0
   Height 1
   Damage 0
   Speed 0
   Scale 0.5
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.50
   States
   {
   Spawn:
      BONE IJKLMNO 2 Bright
      Stop 
   }
}

actor SatyrLightSpawner
{
	+NoBlockMap +NoGravity +NoSector +NoInteraction +NoClip -Solid +CLIENTSIDEONLY
        states
        {
        Spawn:
           Active:
        	TNT1 A 0
        	TNT1 A 0 A_JumpIf(Args[1], "Circle")
        	TNT1 A 2
        	TNT1 A 0 A_SpawnItemEx ("SatyrTrail", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), (0.1)*Random(1, 3), 0, (0.1)*Random(1, 10), random(0,360), 129, Args[2])
                TNT1 A 0 A_SpawnItemEx ("SatyrTrail", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), (0.1)*Random(1, 3), 0, (0.1)*Random(1, 10), random(0,360), 129, Args[2]) 
        	Stop
           Circle:
        	TNT1 A 0
        	TNT1 A 2 A_SpawnItemEx ("SatyrTrail", random(-Args[0], Args[0]), 0, random(0, 8), (0.1)*Random(1, 3), 0, (0.1)*Random(1, 10), Random(0, 360), 129, Args[2]) 
        	Loop
           Inactive:
		TNT1 A 1
		Loop
        }     
}

ACTOR SatyrBow
{   
   Radius 8
   Height 8
   Speed 1
   PROJECTILE
   RENDERSTYLE Normal
   -NOGRAVITY
   +LOWGRAVITY
   States
   {
   Spawn:
      STYR R 10 BRIGHT
      goto Death 
   Death:
      STYR R -1 
   }
}