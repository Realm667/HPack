actor DragonFamiliar 3106
{
	//$Category Monsters
	//$Title Dragon Familiar
    Health 25
    Radius 16
    Height 32
    Speed 10
    PainChance 200
    Monster 
    +NOGRAVITY
    +FLOAT
    MaxTargetRange 376
    SeeSound "dragonfam/see"
    PainSound "dragonfam/pain"
    DeathSound "dragonfam/death"
    ActiveSound "dragonfam/idle"
    Obituary "$OB_DRAGONFAMILIAR"
    States
    {
    Spawn:
        DFAM ABCD 10 A_Look
        Loop
    See:
        DFAM AABBCCDD 3 A_Chase
        Loop
    Missile:
        DFAM E 0 A_PlaySound("dragonfam/attack")
        DFAM E 0 A_Jump(208,7)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
        Goto See
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
        Goto Missile+1
    Pain:
        DFAM F 2
        DFAM F 2 A_Pain
        Goto See
	Pain.HandOfTheWraith:
		DFAM F 0 A_GiveInventory("WraithHandShadow", 1) //+Ghost flag and translucency
		DFAM F 0 A_GiveInventory("WraithHandDamage") //Vulnerability
        DFAM F 2
        DFAM F 2 A_Pain
		Goto See
    Death:
        DFAM G 0 A_Die
        DFAM G 8 A_NoBlocking 
        DFAM H 8 A_Scream
        DFAM I 6 A_SetFloorClip
        DFAM JKL 6
        DFAM M -1
        Stop
    }
}

actor MiniFirePuff
{
    Radius 6
    Height 10
    Speed 7
    FastSpeed 14
    Damage 1
    DamageType Fire
    +STRIFEDAMAGE
    Projectile
    RenderStyle Add
    ALPHA 0.67
    States
    {
    Spawn:
        DFFP ABCDE 2 Bright Light("MiniFire")
        DFFP F 2 Bright Light(" MiniFireX_1")
        DFFP G 2 Bright Light(" MiniFireX_2")
        DFFP H 2 Bright Light(" MiniFireX_3")
    Death:
        DFFP IJK 2 Bright Light(" MiniFireX_1")
        DFFP LMN 2 Bright Light(" MiniFireX_2")
        DFFP OPQR 2 Bright Light(" MiniFireX_3")
        Stop
    }
}
actor MiniFirePuffNoDamage : MiniFirePuff { Damage 0 }
